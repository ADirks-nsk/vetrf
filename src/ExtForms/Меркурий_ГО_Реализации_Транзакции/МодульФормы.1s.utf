Перем ГМ;
Перем ГМ2; // Глобальный модуль для Ветис 2.0
Перем цвКрасный, цвЖелтый, цвЗеленый, цвГолубой, цвФиолетовый;
Перем КаталогОбработки;
Перем ВыбСтрока;
Перем Филиал;
Перем оПривязки; //:Меркурий.Привязки
Перем ТПДокументов, СписокДокументов;

Перем ВыбФирма Экспорт;

Процедура ПриНачалеВыбораЗначения(ЭлементДиалога,ФлагСтандОбр)
	Если ЭлементДиалога = "Отправитель_Площадка" Тогда
		ВремЭлем = Отправитель_ХозСубъект;
		ОткрытьФорму("Справочник.ВСД_Площадка",ВремЭлем);
		ФлагСтандОбр = 0;		
	КонецЕсли;
КонецПроцедуры // ПриНачалеВыбораЗначения()

//=========================== СЛУЖЕБНЫЕ ФУНКЦИИ =============================

Процедура ОбновитьИнф()
	
	Форма.Инфо.Заголовок("Документов: "+СписокДокументов.КоличествоСтрок()+", количество: "+Окр(СписокДокументов.Итог("Колво"))+" ");
	
	ВыбКолво =0;
	ВыбДок = 0;
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка = 1 Тогда 
			ВыбКолво = ВыбКолво + (СписокДокументов.Колво);
			ВыбДок=ВыбДок+1;
		КонецЕсли;
	КонецЦикла;	      
	ВыбКолво = Окр(ВыбКолво);

	Форма.ИнфоВыбор.Заголовок("Выбрано "+ВыбДок+" документов, количество = "+ВыбКолво+" ");
	
КонецПроцедуры

Процедура ЗаголовокНадпись()
	
	форма.НачалоПериодаТекст.Заголовок(СокрЛП(НачДата));
	форма.КонецПериодаТекст.Заголовок(сокрлп(КонДата));

КонецПроцедуры // ЗаголовокНадпись

Процедура РаскраситьСписокПартий()
	СписокПартий.ВыбратьСтроки();
	Пока СписокПартий.ПолучитьСтроку() = 1 Цикл		

		Если ДобавлятьУпаковки = 1 Тогда 		
		
			Если (СписокПартий.Колво = 0 ) или (СписокПартий.КолвоМест=0) Тогда 
				СписокПартий.сЦвет = цвКрасный;	
			ИначеЕсли (СписокПартий.Колво >= СписокПартий.КолвоСписания) и 
				(СписокПартий.КолвоМест >= СписокПартий.КолвоМестСписания) Тогда
					СписокПартий.сЦвет = цвЗеленый;
			Иначе			
				СписокПартий.сЦвет = цвЖелтый;							
			КонецЕсли;
		Иначе
			Если (СписокПартий.Колво = 0 ) Тогда 
				СписокПартий.сЦвет = цвКрасный;	
			ИначеЕсли (СписокПартий.Колво >= СписокПартий.КолвоСписания) Тогда
					СписокПартий.сЦвет = цвЗеленый;
			Иначе			
				СписокПартий.сЦвет = цвЖелтый;							
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


Процедура Клик_партии()
	текСтр = СписокПартий.ТекущаяСтрока();
	текКол = СписокПартий.ТекущаяКолонка();
	
	Если текКол="Пометка" Тогда		
		Пометка = СписокПартий.ПолучитьЗначение(ТекСтр,"Пометка");
		Если Пометка = 2 Тогда
			СписокПартий.Пометка=1;
		Иначе
			СписокПартий.Пометка=2;
		КонецЕсли;
	ИначеЕсли текКол="Партия" Тогда 
		ВыбСтрока = текСтр;
		ТекПартия = СписокПартий.ПолучитьЗначение(ТекСтр,текКол);
		Тек_ВСД_Продукция_Элемент = СписокПартий.ПолучитьЗначение(ТекСтр,"ВСД_Продукция_Элемент");

		спМеню = СоздатьОбъект("СписокЗначений");		
		спМеню.ДобавитьЗначение("ВСД_Продукция_Элемент", "по тек. элементу");
		спМеню.ДобавитьЗначение("Площадка", "по площадке");

		КонтекстОтбора = СоздатьОбъект("СписокЗначений");
		
		зн="";
		спМеню.ВыбратьЗначение(зн,,,,1);
		Если зн="ВСД_Продукция_Элемент" Тогда 
			КонтекстОтбора.ДобавитьЗначение(Тек_ВСД_Продукция_Элемент,"ВСД_Продукция_Элемент");
		КонецЕсли;
		КонтекстОтбора.ДобавитьЗначение(Отправитель_Площадка, "Площадка");
		КонтекстОтбора.ДобавитьЗначение(Отправитель_ХозСубъект, "ХозСубъект");			
				
		ОткрытьПодбор("Справочник.ВСД_Партия", "ФормаСписка",КонтекстОтбора,0, текПартия);
		
	Иначе
		Эл = СписокПартий.ПолучитьЗначение(текСтр, текКол);	
		ОткрытьФорму(Эл);
	КонецЕсли;
	
	ОбновитьИнф();	
КонецПроцедуры

Процедура ВыбратьПартию_ВСД_Производство()
	СпрПартия = СоздатьОбъект("Справочник.ВСД_Партия");
	
	СписокПартий.ВыбратьСтроки();
	Пока СписокПартий.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(СписокПартий.ВСД_Производство)=1 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СписокПартий.ВСД_Производство.Проведен()=1 Тогда 
			Если СпрПартия.ВыбратьЭлементыПоРеквизиту("ДокОснование", СписокПартий.ВСД_Производство, 0, 0)=1 Тогда 
				Пока СпрПартия.ПолучитьЭлемент()=1 Цикл
					Если СпрПартия.ПометкаУдаления()=1 Тогда 
						Продолжить;
					КонецЕсли;
					Если СпрПартия.Продукция_Элемент <> СписокПартий.ВСД_Продукция_Элемент Тогда
						Продолжить;
					КонецЕсли;
					
					СписокПартий.Партия = СпрПартия.ТекущийЭлемент();
					СписокПартий.Колво = СписокПартий.Партия.Количество;
					Попытка
						СписокПартий.КолвоМест = СписокПартий.Партия.КоличествоМест;
					Исключение
					КонецПопытки;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	РаскраситьСписокПартий();
КонецПроцедуры

//======================================================================
Процедура ТПДокументовПриВыбореФлажка(ТабличноеПоле,Стр, Колонка, ТипРегиона)
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, СписокДокументов);
	СписокДокументов.Пометка = ?(СписокДокументов.Пометка = 1,0,1);
	ТабличноеПоле.ОбновитьСтроки();
КонецПроцедуры

//======================================================================
Процедура ТПДокументовПриАктивизацииСтроки(ТабличноеПоле)
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, СписокДокументов);
КонецПроцедуры

Процедура ТПДокументовВыбор()
	ГМ.ПриАктивизацииСтрокиТП(ТПДокументов, СписокДокументов);
	ГМ.ПриАктивизацииКолонкиТП(ТПДокументов, СписокДокументов);
	
	текСтр=СписокДокументов.ТекущаяСтрока();
	Если текСтр=0 Тогда Возврат; КонецЕсли;
	текКол = СписокДокументов.ТекущаяКолонка();
	
	Если текКол="Площадка" Тогда 
		ВыбСтрока = текСтр;
		Площадка = СписокДокументов.ПолучитьЗначение(ТекСтр,текКол);
		
		ХозСубъект = СписокДокументов.ПолучитьЗначение(текСтр, "ХозСубъект");
					
		тз = ГМ.ВыбратьВсеПлощадкиХС(ХозСубъект);
		
		СписокОтбора = СоздатьОбъект("СписокЗначений");
		
		Тз.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСТроку()=1 Цикл
			СписокОтбора.ДобавитьЗначение(тз.id);
		КонецЦикла;			
				
		ОткрытьПодбор("Справочник.ВСД_Площадка", "ФормаСписка", СписокОтбора,0, Площадка);
		
	ИначеЕсли текКол="ХозСубъект" Тогда 
		
		ХозСубъект = СписокДокументов.ПолучитьЗначение(текСтр, "ХозСубъект");	
		Если ПустоеЗначение(ХозСубъект)=1 Тогда 
			ОткрытьФорму("Справочник.ВСД_ХозСубъект");
		Иначе
			ОткрытьФорму(ХозСубъект);
		КонецЕсли;
		
	Иначе
		Эл = СписокДокументов.ПолучитьЗначение(текСтр, текКол);	
		ОткрытьФорму(Эл);
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ТПДокументовПриВыводеСтроки(ТабличноеПоле,ОформлениеСтроки,ДанныеСтроки,ТипРегиона)

	Если ТипРегиона = 3 Тогда
		ГМ.ВывестиФлажок(ОформлениеСтроки, ДанныеСтроки, "Пометка");
		
		Если (ПустоеЗначение(ДанныеСтроки.ВСД)=0) Тогда
			Если СокрЛП(ДанныеСтроки.ВСД.Статус)="COMPLETED" Тогда
				ОформлениеСтроки.ЦветФона = 65280;	// Зеленый
			КонецЕсли;
		ИначеЕсли ПустоеЗначение(ДанныеСтроки.ХозСубъект.GUID)=1 Тогда 			
			ОформлениеСтроки.ЦветФона = 8421631;	// Красный
		ИначеЕсли (ПустоеЗначение(ДанныеСтроки.Площадка)=0) Тогда
			Если (ПустоеЗначение(ДанныеСтроки.Площадка.GUID)=0)  Тогда 
				//ДанныеСтроки.сЦвет = "";							
			КонецЕсли;
		Иначе			
			ОформлениеСтроки.ЦветФона = 8454143;	// Желтый
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//=========================== ПЕЧАТНЫЕ ФОРМЫ =============================

Процедура Реестр()
	Таб = СоздатьОбъект("Таблица");
	Таб.ВывестиСекцию("Шапка|Осн");
	Если ДобавлятьУпаковки = 1 Тогда
		Таб.ПрисоединитьСекцию("Шапка|ВесМест");
	КонецЕсли;
	Таб.ПрисоединитьСекцию("Шапка|Хвост");
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		
		Таб.ВывестиСекцию("Строка|Осн");
		Если ДобавлятьУпаковки = 1 Тогда
			Таб.ПрисоединитьСекцию("Строка|ВесМест");
		КонецЕсли;
		Таб.ПрисоединитьСекцию("Строка|Хвост");
	КонецЦикла;

	Таб.Опции(0,0,0,0);
	Таб.ТолькоПросмотр();
	Таб.Показать("Реестр");	
КонецПроцедуры
              
Процедура ПечатьСокрВСД()
	
	тз = СоздатьОбъект("ТаблицаЗначений");
	СписокДокументов.Выгрузить(тз);
	тз.ВыбратьСтроки();
	Пока тз.ПолучитьСтроку() = 1 Цикл
		Если (тз.Пометка<>1) или (Пустоезначение(тз.ВСД)=1) Тогда
			Продолжить;	
		КонецЕсли;
		ГМ.ПечатьСокрВСД(тз.ВСД.ТекущийДокумент());
	КонецЦикла;
КонецПроцедуры

Процедура МенюПечать()
	спМеню = СоздатьОбъект("СписокЗначений");
	спМеню.ДобавитьЗначение("Реестр", "Реестр");
	спМеню.ДобавитьЗначение("СокрВСД", "Сокращенный ВСД");
	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "Реестр" Тогда 
		Реестр();
	ИначеЕсли зн = "СокрВСД" Тогда 
		ПечатьСокрВСД();
	КонецЕсли;
		
КонецПроцедуры



//=========================== ФУНКЦИИ КНОПОК =============================

Процедура ЗаполнитьСписокДокументов_Реализации() 

	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.ДобавитьЗначение(НачДата, "НачДата");
	СписокПараметров.ДобавитьЗначение(КонДата, "КонДата");
	СписокПараметров.ДобавитьЗначение(ВыбКонтрагент, "ВыбКонтрагент");
	СписокПараметров.ДобавитьЗначение(ВыбФирма, "ВыбФирма");
	СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,"Площадка"); //ЖД для фильтра по складу	
	
    Если  ФС.СуществуетФайл(КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_ЗаполнитьСписокДокументов.ert")=1 Тогда
		//{ переопределение функции заполнения списка документов 
		// интеграция переопределяется в \Меркурий_Подключаемый_ЗаполнитьСписокДокументов.ert
		//
		ОткрытьФормуМодально("Отчет",СписокПараметров,КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_ЗаполнитьСписокДокументов.ert");
		
		тз = "";
		
		Если ТипЗначенияСтр(СписокПараметров)  = "СписокЗначений" Тогда				
			тз = СписокПараметров.Получить("ТЗ");				
		Иначе
			Возврат;
		КонецЕсли; 			
		Если ПустоеЗначение(ТЗ)=1 Тогда
			Предупреждение("Отсутствуют документы Реализации за выбранный период","!");
			Возврат;
		КонецЕсли;
		//}
	Иначе // стандартный запрос
		тз = ГМ.ПолучитьТзРеализаций( СписокПараметров ); //ЖД изменить ГМ - добавить Параметр Отправитель_Площадка, и если к ней привязан склад, фильтровать
	КонецЕсли;

	ТекСтрока = СписокДокументов.ТекущаяСтрока();
	
	СписокДокументов.УдалитьСтроки();
	
	Если ТипЗначенияСтр(тз) = "ТаблицаЗначений" Тогда
		тз.ВыбратьСтроки();
		Пока тз.ПолучитьСтроку() = 1 Цикл
			СписокДокументов.НоваяСтрока();
			СписокДокументов.Пометка = 0;
			СписокДокументов.Грузополучатель = тз.Грузополучатель;
			СписокДокументов.ВСД = тз.ДокВСД;
			СписокДокументов.Колво = тз.Количество;
			Попытка СписокДокументов.КолвоМест = тз.КоличествоМест; Исключение КонецПопытки;
			СписокДокументов.Док = тз.Док;
			СписокДокументов.Контрагент = тз.Контрагент;
	
			СписокДокументов.ХозСубъект = ГМ.НайтиХозСубъект(тз.Контрагент);
			Если ПустоеЗначение(СписокДокументов.ХозСубъект)=0 Тогда
				СписокДокументов.Площадка = ГМ.НайтиПлощадкуПоКонтрагенту( СписокДокументов.Грузополучатель);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		ТПДокументов.ОбновитьСтроки();
	Исключение
	КонецПопытки;
	      
	//РаскраситьСписокДокументов( тзДок, ТекСтрока );
	ОбновитьИнф();
КонецПроцедуры

процедура ВыбратьПериод()
	если ВвестиПериод(НачДата,КонДата)=1 тогда
		ЗаголовокНадпись();
		СписокДокументов.УдалитьСтроки();
		ЗаполнитьСписокДокументов_Реализации();
	конецесли;
конецпроцедуры

Процедура СоздатьВСД()
	
	Если СписокПартий.КоличествоСтрок()=0 Тогда
		Сообщить("Не заполнена таблица партий.","!");
		Возврат;
	КонецЕсли;
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка<>1 Тогда
			Продолжить;
		КонецЕсли;

		Если (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда       
			//Если (СокрЛП(СписокДокументов.ВСД.Статус)= "COMPLETED") Тогда  
			//	Сообщить("["+СписокДокументов.Док+"] партия ["+Партия+"] отправлен "+СписокДокументов.ВСД,"!");
			//	Продолжить;
			//КонецЕсли;
			
			Если (ПустоеЗначение(СписокДокументов.ВСД.Статус) = 1) Тогда 
				Сообщить("Для "+СписокДокументов.Док+" ВСД уже создан, но не отправлен","i");
				Продолжить;
			КонецЕсли;		
		КонецЕсли;		

		Состояние("Заполнение списка партий ВСД ");
				
		//Если СписокДокументов.Контрагент.Вид()="Склады" Тогда 
		//	ХС = Отправитель_ХозСубъект;
		//Иначе
		//	ХС = ГМ.НайтиХозСубъект( СписокДокументов.Контрагент );		
		//КонецЕсли;
		//Если СписокДокументов.Грузополучатель.Вид()="Склады" Тогда
		//	Площадка = ГМ.НайтиПлощадкуПоСкладу(СписокДокументов.Грузополучатель);
		//Иначе
		//	Площадка = ГМ.НайтиПлощадкуПоКонтрагенту( СписокДокументов.Грузополучатель );
		//КонецЕсли;
		ХС = СписокДокументов.ХозСубъект;
		Площадка = СписокДокументов.Площадка;
		
		// создание кастомного ВСД
		Если ФС.СуществуетФайл(КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_транзакция.ert")=1 Тогда
			
			//{ переопределение функции создания ВСД 
			// интеграция переопределяется в Меркурий_Подключаемый_Создание_ВСД_транзакция.ert
			//
			СписокПараметров = СоздатьОбъект("СписокЗначений");
			СписокПараметров.ДобавитьЗначение(СписокДокументов.Док,				"ДокОснование");    
			СписокПараметров.ДобавитьЗначение(СписокДокументов.Контрагент,		"Контрагент");    
			СписокПараметров.ДобавитьЗначение(СписокДокументов.Грузополучатель,	"Грузополучатель");    
			СписокПараметров.ДобавитьЗначение(Отправитель_ХозСубъект,			"Отправитель_ХозСубъект");    
			СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,				"Отправитель_Площадка");    
			СписокПараметров.ДобавитьЗначение(Перевозчик_ХозСубъект,			"Перевозчик_ХозСубъект");    
			СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,				"Отправитель_Площадка");    
			СписокПараметров.ДобавитьЗначение( ХС,				"ХС");    
			СписокПараметров.ДобавитьЗначение( Площадка,		"Площадка");    
			СписокПараметров.ДобавитьЗначение( СписокДокументов.Колво,			"Колво");    
			Попытка СписокПараметров.ДобавитьЗначение( СписокДокументов.КолвоМест,		"КолвоМест"); Исключение КонецПопытки;    
			//СписокПараметров.ДобавитьЗначение( ВСД_Продукция_Свойство,			"ВыбВидСвойств");
			
			СписокПараметров.ДобавитьЗначение( ВСД_Экспертиза,					"ВСД_Экспертиза");    
			СписокПараметров.ДобавитьЗначение( ВСД_Местность,					"ВСД_Местность");
			СписокПараметров.ДобавитьЗначение( ВСД_ОсобыеОтметки,				"ВСД_ОсобыеОтметки");    
			
			СписокПараметров.ДобавитьЗначение( СписокПартий,					"СписокПартий");    
				
			ОткрытьФормуМодально("Отчет",СписокПараметров,КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_транзакция.ert");
			
			ДокументСсылка = "";
			
			Если ТипЗначенияСтр(СписокПараметров)  = "СписокЗначений" Тогда				
				ДокументСсылка = СписокПараметров.Получить("ДокументСсылка");				
			КонецЕсли;  
			
			Если ПустоеЗначение(ДокументСсылка)=1 Тогда
				Предупреждение("В подключаемом модуле не удалось создать ВСД","!");
				//Возврат "";
			КонецЕсли;
			//}
			
		Иначе
			// стандартное создание ВСД
				
			ДокВСД = СоздатьОбъект("Документ.ВСД_транзакция");
			ДокВСД.Новый();
			ДокВСД.ДокОснование = СписокДокументов.Док;			
			ДокВСД.ДатаДок = ДокВСД.ДокОснование.ДатаДок;			
			ДокВСД.Отправитель_ХозСубъект = Отправитель_ХозСубъект;
			ДокВСД.Отправитель_Площадка = Отправитель_Площадка;			
			ДокВСД.Получатель_ХозСубъект = ХС;		
			ДокВСД.Получатель_Площадка = Площадка;					
			ДокВСД.Перевозчик_ХозСубъект = Перевозчик_ХозСубъект;
						
			ДокВСД.ТтнСерия = ""; //ТТН.Серия;
			ДокВСД.ТтнНомер = СписокДокументов.Док.НомерДок;
			ДокВСД.ТтнДата = СписокДокументов.Док.ДатаДок;
			ДокВСД.номерАвто = ГМ.ПолучитьНомерАвто(ДокВСД.ДокОснование);
			
			Автор = ГМ.ПолучитьАвтора();
			Попытка	ДокВСД.Автор = Автор;	Исключение 	КонецПопытки;
			Попытка	ДокВСД.Филиал = Автор.Филиал;	Исключение	КонецПопытки;
				
			ДокВСД.ФормаВСД = 1;			
			ДокВСД.Экспертиза 		= ВСД_Экспертиза;
			ДокВСД.Местность 		= ВСД_Местность;
			ДокВСД.ОсобыеОтметки 	= ВСД_ОсобыеОтметки;
			ДокВСД.cargoExpertized  = 1;
			ДокВСД.cargoInspected  	= 1;			
			
			тз = ГМ.СвернутьТч(ДокВСД.ДокОснование);
			
			тз.ВыбратьСтроки();
			Пока тз.ПолучитьСтроку() = 1 Цикл
				
				стр=0;				
				Если СписокПартий.НайтиЗначение(тз.ВСД_Продукция_Элемент,стр,"ВСД_Продукция_Элемент")>0 Тогда 
					Партия = СписокПартий.ПолучитьЗначение(стр,"Партия");
				Иначе
					Сообщить("Не найдена партия для свойства "+тз.ВСД_Продукция_Элемент+"
					|документ "+ДокВСД.ДокОснование+" пропущен","!");
					Продолжить;
				КонецЕсли;
				ДокВСД.НоваяСтрока();
				ДокВСД.Партия = Партия;
				ДокВСД.Количество = тз.Количество;
				ДокВСД.ЕдиницаИзмерения = ДокВСД.Партия.ЕдиницаИзмерения;
				Если ДобавлятьУпаковки=1 Тогда					
					ДокВСД.КоличествоМест = тз.КоличествоМест;			
					ДокВСД.ФормаУпаковки = ДокВСД.Партия.ФормаУпаковки;
				КонецЕсли;
				
				ДокВСД.Продукция = ДокВСД.Партия.Продукция;
				ДокВСД.ВидПродукции = ДокВСД.Партия.ВидПродукции;

				ДокВСД.Продукция_Элемент 	= тз.ВСД_Продукция_Элемент;
				ДокВСД.ТермическоеСостояние = Макс( ДокВСД.Продукция_Элемент.ТермическиеУсловияПеревозки, 1);
				ДокВСД.НаименованиеПродукции = тз.ВСД_Продукция_Элемент.Наименование;
			КонецЦикла;			
			
			ДокВСД.Записать();
			ДокументСсылка = ДокВСД.ТекущийДокумент();
		КонецЕсли;
		
		СписокДокументов.ВСД = ДокументСсылка;
		
		Сообщить("["+СписокДокументов.Грузополучатель+"] создан документ "+СписокДокументов.ВСД,"i");		
	КонецЦикла;

	//РаскраситьСписокДокументов();		
	
КонецПроцедуры


Процедура ОтменитьВсеДокументы()
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		СписокДокументов.Пометка=1;		
	КонецЦикла;	 
	
	ОбновитьИнф();
КонецПроцедуры

Процедура ВыделитьВсеДокументы()
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.сЦвет = "" Тогда 
			СписокДокументов.Пометка=2;		
		КонецЕсли;
	КонецЦикла;	      

	ОбновитьИнф();	
КонецПроцедуры

Процедура ЗаполнитьПартии()
	
	Если ПустоеЗначение(Отправитель_ХозСубъект)=1 Тогда
		Сообщить("Не выбран параметр Отправитель_ХозСубъект ","!");			
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(Отправитель_Площадка)=1 Тогда
		Сообщить("Не выбран параметр Отправитель_Площадка ","!");			
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(Перевозчик_ХозСубъект)=1 Тогда
		Сообщить("Не выбран параметр Перевозчик_ХозСубъект ","!");			
		Возврат;
	КонецЕсли;
	
	Форма.Закладки.ТекущаяСтрока(2);
	Форма.ИспользоватьСлой("Основной, Партии");
		
	СписокПартий.УдалитьСтроки();
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка<>1 Тогда
			Продолжить;
		КонецЕсли;

		Если (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда       
			//Если (СокрЛП(СписокДокументов.ВСД.Статус)= "COMPLETED") Тогда  
			//	// в накладной несколько видов Продукции - проверяем 
			//	// если ВСД [Completed] для Продукциия - тогда пропускаем
			//	//Если ( СписокДокументов.ВСД.Партия = Партия ) Тогда 
			//		Сообщить("["+СписокДокументов.Док+"] партия ["+Партия+"] отправлен "+СписокДокументов.ВСД,"!");
			//		Продолжить;
			//	//КонецЕсли;
			//КонецЕсли;
			
			//Если (ПустоеЗначение(СписокДокументов.ВСД.Статус) = 1) Тогда 
			//	Сообщить("Для "+СписокДокументов.Док+" ВСД уже создан, но не отправлен","i");
			//	Продолжить;
			//КонецЕсли;		
		КонецЕсли;		

		Состояние("Заполнение списка партий ВСД ");
		
		тз = ГМ.СвернутьТч(СписокДокументов.Док);
				
		тз.ВыбратьСтроки();
		Пока тз.ПолучитьСтроку() = 1 Цикл
			стр=0;
			Если СписокПартий.НайтиЗначение(тз.ВСД_Продукция_Элемент,стр,"ВСД_Продукция_Элемент")>0 Тогда 
				колво = Число(СписокПартий.ПолучитьЗначение(стр, "КолвоСписания"));
				СписокПартий.УстановитьЗначение(стр,"КолвоСписания", колво+тз.Количество);
				Попытка					                                          
					Если ДобавлятьУпаковки = 1 Тогда
						колвоМест = Число(СписокПартий.ПолучитьЗначение(стр, "КолвоМестСписания"));
						СписокПартий.УстановитьЗначение(стр,"КолвоМестСписания", колвоМест+тз.КоличествоМест);
					КонецЕсли;
				Исключение
				КонецПопытки;
			Иначе
				СписокПартий.НоваяСтрока();
				СписокПартий.ВСД_Продукция_Элемент = тз.ВСД_Продукция_Элемент;
				СписокПартий.КолвоСписания = тз.Количество;
				Попытка					
					Если ДобавлятьУпаковки = 1 Тогда
						СписокПартий.КолвоМестСписания = тз.КоличествоМест;
					КонецЕсли;
				Исключение
				КонецПопытки;
			КонецЕсли;

		КонецЦикла;
		
	КонецЦикла;
	
	//Выберем партии автоматом
	СписокПартий.ВыбратьСтроки();
	Пока СписокПартий.ПолучитьСтроку() = 1 Цикл
		Попытка
			СписокПартий.Партия = ГМ.НайтиПервуюПартию(СписокПартий.ВСД_Продукция_Элемент, Отправитель_Площадка, Отправитель_ХозСубъект);
			СписокПартий.Колво = СписокПартий.Партия.Количество;
			Попытка				
				Если СписокПартий.Партия.КоличествоМест = 0 Тогда
				    СписокПартий.КолвоМестСписания = 0;
				Иначе
					СписокПартий.КолвоМест = СписокПартий.Партия.КоличествоМест;
				КонецЕсли;
			Исключение
			КонецПопытки;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	РаскраситьСписокПартий();

КонецПроцедуры


Функция ПроверитьВСД(ВСД)
	Рез =1;
	Если ПустоеЗначение(ВСД)=1 Тогда
		Возврат 0;
	КонецЕсли;
	Если (СокрЛП(ВСД.Статус)="COMPLETED") Тогда
		Возврат 0;
	КонецЕсли;
	Если ПустоеЗначение(ВСД.ТермическоеСостояние)=1 Тогда
		Сообщить("ВСД "+ВСД+" не указано ТермическоеСостояние","!");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
КонецФункции

Процедура ОтправитьВСД()
	
	СписокВСД = СоздатьОбъект("СписокЗначений");
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка = 2 Тогда 		
			Если ПроверитьВСД(СписокДокументов.ВСД)=1 Тогда 
				СписокВСД.ДобавитьЗначение(СписокДокументов.ВСД);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации("Меркурий отправка исходящих ВСД ");

	ГМ.ОтправитьВсе_ВСД_Транзакция(СписокВСД, НачДата, КонДата);
	
	ТПДокументов.ОбновитьСтроки();
	ОбновитьИнф();	
КонецПроцедуры

Процедура ПолучитьПартии()
	ЗаписьЖурналаРегистрации("Меркурий получение партий ");
	
	ГМ.ПолучитьПартии(Отправитель_Площадка);
	
КонецПроцедуры

Процедура Создать_ВСД_Производство()
	
	СписокПартий.ВыбратьСтроки();
	Пока СписокПартий.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(СписокПартий.Партия)=0 Тогда
			Сообщить("В строке партий № "+СписокПартий.НомерСтроки+" указана партия. Пропускаем...");
			Продолжить;
		КонецЕсли;

		//Если (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда       
		//	//Если (СокрЛП(СписокДокументов.ВСД.Статус)= "COMPLETED") Тогда  
		//	//	Сообщить("["+СписокДокументов.Док+"] партия ["+Партия+"] отправлен "+СписокДокументов.ВСД,"!");
		//	//	Продолжить;
		//	//КонецЕсли;
		//	
		//	Если (ПустоеЗначение(СписокДокументов.ВСД.Статус) = 1) Тогда 
		//		Сообщить("Для "+СписокДокументов.Док+" ВСД уже создан, но не отправлен","i");
		//		Продолжить;
		//	КонецЕсли;		
		//КонецЕсли;		

		Состояние("Создание ВСД_Производство ");
				
		
		Если ФС.СуществуетФайл(КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_Производство.ert")=1 Тогда
			
			//{ переопределение функции создания ВСД 
			// интеграция переопределяется в Меркурий_Подключаемый_Создание_ВСД_Производство.ert
			//
			
			СписокПараметров = СоздатьОбъект("СписокЗначений");
			СписокПараметров.ДобавитьЗначение(Отправитель_ХозСубъект,				"Отправитель_ХозСубъект");    
			СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,					"Отправитель_Площадка");    
			СписокПараметров.ДобавитьЗначение( СписокПартий.КолвоСписания,			"Колво");    
			Попытка СписокПараметров.ДобавитьЗначение( СписокПартий.КолвоМестСписания,		"КолвоМест"); Исключение КонецПопытки;
			СписокПараметров.ДобавитьЗначение( СписокПартий.ВСД_Продукция_Элемент,	"Продукция_Элемент");    
			//
			СписокПараметров.ДобавитьЗначение( ВСД_Экспертиза,		"ВСД_Экспертиза");    
			СписокПараметров.ДобавитьЗначение( ВСД_Местность,		"ВСД_Местность");
			СписокПараметров.ДобавитьЗначение( ВСД_ОсобыеОтметки,	"ВСД_ОсобыеОтметки");    

			СписокПараметров.ДобавитьЗначение( НачДата,				"НачДата");
			СписокПараметров.ДобавитьЗначение( КонДата,				"КонДата");    
			
			ОткрытьФормуМодально("Отчет",СписокПараметров,КаталогОбработки+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_Производство.ert");
			
			ДокументСсылка = "";
			
			Если ТипЗначенияСтр(СписокПараметров)  = "СписокЗначений" Тогда
				
				ДокументСсылка = СписокПараметров.Получить("ДокументСсылка");
				
			КонецЕсли;  
			
			Если ПустоеЗначение(ДокументСсылка)=1 Тогда
				Предупреждение("В подключаемом модуле не удалось создать ВСД","!");
				//Возврат "";
			КонецЕсли;
			//}
		
		Иначе
			
			ДокВСД = СоздатьОбъект("Документ.ВСД_Производство");
			ДокВСД.Новый();
			//ДокВСД.ДатаДок = ДокВСД.ДокОснование.ДатаДок;
			ДокВСД.ДатаДок = НачДата;			
			
			ДокВСД.Производитель_ХозСубъект = Отправитель_ХозСубъект;
			ДокВСД.Производитель_площадка = Отправитель_Площадка;
			
			Автор = ГМ.ПолучитьАвтора();
			Попытка				
				ДокВСД.Автор = Автор;
			Исключение
			КонецПопытки;
			Попытка				
				ДокВСД.Филиал = Автор.Филиал;
			Исключение
			КонецПопытки;
			
			ДокВСД.Экспертиза 		= ВСД_Экспертиза;
			ДокВСД.Местность 		= ВСД_Местность;
			ДокВСД.ОсобыеОтметки 	= ВСД_ОсобыеОтметки;
			ДокВСД.cargoExpertized  = 1;
							
			ДокВСД.НоваяСтрока();
			
			ДокВСД.Продукция_Элемент 	= СписокПартий.ВСД_Продукция_Элемент;
			ДокВСД.Количество 			= СписокПартий.КолвоСписания;
			Попытка ДокВСД.КоличествоМест 		= СписокПартий.КолвоМестСписания;Исключение КонецПопытки;					
			ДокВСД.ЕдиницаИзмерения 	= ДокВСД.Продукция_Элемент.ЕдиницаИзмерения;
			ДокВСД.ФормаУпаковки 		= ДокВСД.Продукция_Элемент.ФормаУпаковки;
			ДокВСД.Продукция 			= ДокВСД.Продукция_Элемент.Продукция;
			ДокВСД.ВидПродукции 		= ДокВСД.Продукция_Элемент.ВидПродукции;
			ДокВСД.ВидДвижения 			= 1;
			ДокВСД.НаименованиеПродукции = ДокВСД.Продукция_Элемент.Наименование;
			
			
			ДокВСД.ЗавершитьОперацию = 1;
			ДокВСД.ДатаИзготовления1 = НачДата;
			Если КонДата>НачДата Тогда
				ДокВСД.ДатаИзготовления2 = КонДата;
			КонецЕсли;
			ДокВСД.ДатаСрокГодности1 = ДокВСД.ДатаИзготовления1+ДокВСД.Продукция_Элемент.СрокГодности;
				
			
			ДокВСД.Записать();
			ДокументСсылка = ДокВСД.ТекущийДокумент();
		КонецЕсли;
		
		СписокПартий.ВСД_Производство = ДокументСсылка;
		
		Сообщить(" создан документ "+СписокПартий.ВСД_Производство,"i");		
	КонецЦикла;
	
КонецПроцедуры


Функция ПроверитьВСД_Производство(ВСД)
	Рез =1;
	Если ПустоеЗначение(ВСД)=1 Тогда
		Возврат 0;
	КонецЕсли;
	Если (СокрЛП(ВСД.Статус)="COMPLETED") Тогда
		Возврат 0;
	КонецЕсли;
	//Если ПустоеЗначение(ВСД.ТермическоеСостояние)=1 Тогда
	//	Сообщить("ВСД "+ВСД+" не указано ТермическоеСостояние","!");
	//	Возврат 0;
	//КонецЕсли;
	
	Возврат 1;
КонецФункции


Процедура Отправить_ВСД_Производство()

	СписокВСД = СоздатьОбъект("СписокЗначений");
	СписокПартий.ВыбратьСтроки();
	Пока СписокПартий.ПолучитьСтроку() = 1 Цикл
		Если ПроверитьВСД_Производство(СписокПартий.ВСД_Производство)=1 Тогда 
			СписокВСД.ДобавитьЗначение(СписокПартий.ВСД_Производство);
		КонецЕсли;
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации("Меркурий отправка ВСД Производство");
	
	ГМ.ОтправитьВсе_ВСД_Производство(СписокВСД, НачДата, КонДата);

	ВыбратьПартию_ВСД_Производство();
КонецПроцедуры

Процедура Очистить_Партию()
	СписокПартий.Партия="";	
КонецПроцедуры


Процедура АннулироватьВСД()
	Если Вопрос("Вы уверены, что хотите аннулировать выбранные ВСД?",4,30)=6 Тогда 
		Сообщить("Выполняется аннулирование ВСД");
		
		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			Если СписокДокументов.Пометка = 2 Тогда 
				ГМ.Аннулировать_ВСД_транзакция(СписокДокументов.ВСД);
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьСписокДокументов_Реализации();
	КонецЕсли;
КонецПроцедуры

Процедура Создать_Инвентаризацию()
		Состояние("Создание ВСД2_Инвентаризация ");
			ДокВСД = СоздатьОбъект("Документ.ВСД2_Инвентаризация");
			ДокВСД.Новый();
			ДокВСД.ДатаДок = НачДата;			
			ДокВСД.Фирма=ВыбФирма;
			ДокВСД.Владелец_ХозСубъект = Отправитель_ХозСубъект;
			ДокВСД.Владелец_площадка = Отправитель_Площадка;
			
			Попытка	ДокВСД.Автор = ГМ.ПолучитьАвтора();	Исключение 	КонецПопытки;
			Попытка	ДокВСД.Филиал = ДокВСД.Автор.Филиал;	Исключение	КонецПопытки;			
			Попытка ДокВСД.Фирма 		= ВыбФирма; Исключение КонецПопытки;

	СписокПартий.ВыбратьСтроки();
	Пока СписокПартий.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(СписокПартий.Партия)=0 Тогда
			Сообщить("В строке партий № "+СписокПартий.НомерСтроки+" указана партия. Пропускаем...");
			Продолжить;
		КонецЕсли;


			ДокВСД.НоваяСтрока();
			
			ДокВСД.Продукция_Элемент 	= СписокПартий.ВСД_Продукция_Элемент;
			ДокВСД.Количество 			= СписокПартий.КолвоСписания;
			Попытка ДокВСД.КоличествоМест 		= СписокПартий.КолвоМестСписания;Исключение КонецПопытки;					
		
			ДокВСД.Продукция 			= ДокВСД.Продукция_Элемент.Продукция;
			ДокВСД.ВидПродукции 		= ДокВСД.Продукция_Элемент.ВидПродукции;
		    ДокВСД.ЕдиницаИзмерения 	= ДокВСД.Продукция_Элемент.ЕдиницаИзмерения;
			ДокВСД.НаименованиеПродукции = ДокВСД.Продукция_Элемент.Наименование;
			ДокВСД.ДатаИзготовления1 = НачДата;
			Если КонДата>НачДата Тогда
				ДокВСД.ДатаИзготовления2 = КонДата;
			КонецЕсли;
			ДокВСД.ДатаСрокГодности1 = ДокВСД.ДатаИзготовления1+ДокВСД.Продукция_Элемент.СрокГодности;
			ДокВСД.Производитель_Страна	=гм.СписокКонстант.Получить("Страна");
			ДокВСД.Производитель_площадка=Отправитель_Площадка;
			ДокВСД.Записать();
			ДокументСсылка = ДокВСД.ТекущийДокумент();
		СписокПартий.ВСД_Производство = ДокументСсылка;
		
		Сообщить(" создан документ "+СписокПартий.ВСД_Производство,"i");		
	КонецЦикла;
	
КонецПроцедуры//+

// ==================== МЕНЮ кнопок ===========================

Процедура МенюПлощадки()
		
	//меню
	СписокДействийВСД = СоздатьОбъект("СписокЗначений");
	СписокДействийВСД.ДобавитьЗначение("ЗагрузитьПоХС","Загрузить площадки по ХС");
	СписокДействийВСД.ДобавитьЗначение("НайтиПоИмени","Найти площадки по названию");
	СписокДействийВСД.ДобавитьЗначение("СоздатьХС","Создать ХозСубъект");
	СписокДействийВСД.ДобавитьЗначение("СоздатьПлощадку","Создать Площадку");
		
	стр=0; Зн="";
	Если СписокДействийВСД.ВыбратьЗначение(Зн, "", стр, 60, 1) = 1 Тогда	
		
		Если Зн ="ЗагрузитьПоХС"  Тогда 
			ГМ.ЗагрузитьПлощадки(СписокДокументов.ХозСубъект);		
			ТПДокументов.ОбновитьСтроки();
		ИначеЕсли Зн = "НайтиПоИмени" Тогда 
			Попытка				
				ГМ2.НайтиПлощадкиПоНазванию( СписокДокументов.ХозСубъект, Регион, 1);		
			Исключение
				//если нет ГМ2 - вызываем ГМ1
				ГМ.НайтиПлощадкиПоНазванию( СписокДокументов.ХозСубъект, Регион, 1);		
			КонецПопытки;
			ТПДокументов.ОбновитьСтроки();
		ИначеЕсли Зн = "СоздатьХС" Тогда 
			ГМ.СоздатьХС( СписокДокументов );		
			ТПДокументов.ОбновитьСтроки();
		ИначеЕсли Зн = "СоздатьПлощадку" Тогда 
			ГМ.СоздатьПлощадку( СписокДокументов );		
			ТПДокументов.ОбновитьСтроки();
		КонецЕсли;
	КонецЕсли;
	
	//ПриВыбореЗакладки(Форма.Закладки.ТекущаяСтрока(),"Реализации");
КонецПроцедуры

Процедура МенюВСД()
	спМеню = СоздатьОбъект("СписокЗначений");
	спМеню.ДобавитьЗначение("СоздатьВСД", "Создать ВСД");
	спМеню.ДобавитьЗначение("ОтправитьВСД", "Отправить ВСД");
	спМеню.ДобавитьЗначение("АннулироватьВСД", "Аннулировать ВСД");
	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "СоздатьВСД" Тогда 
		СоздатьВСД();
	ИначеЕсли зн = "ОтправитьВСД" Тогда 
		ОтправитьВСД();
	ИначеЕсли зн = "АннулироватьВСД" Тогда 
		АннулироватьВСД();
	КонецЕсли;
		
КонецПроцедуры

Процедура МенюПроизводство()
	спМеню = СоздатьОбъект("СписокЗначений");
	спМеню.ДобавитьЗначение("Очистить_Партию", "Очистить выбранную партию");
	спМеню.ДобавитьЗначение("Создать_ВСД_Производство", "Создать ВСД Производство");
	спМеню.ДобавитьЗначение("Отправить_ВСД_Производство", "Отправить ВСД Производство");
	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "Очистить_Партию" Тогда 
		Очистить_Партию();
	ИначеЕсли зн = "Создать_ВСД_Производство" Тогда 
		Создать_ВСД_Производство();
	ИначеЕсли зн = "Отправить_ВСД_Производство" Тогда 
		Отправить_ВСД_Производство();
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриИзмененииФирмы()
	ГМ.Инициализация(Контекст);        
	ГМ.ЗагрузитьПараметрыВФорму(Контекст);

	СписокДокументов.УдалитьСтроки();
	СписокПартий.УдалитьСтроки();
	ЗаполнитьСписокДокументов_Реализации();	
КонецПроцедуры

//======================================================================
Процедура ПриИзмененииСФ()
	Если СписокФирм.ТекущаяСтрока() <> 0 Тогда
		ВыбФирма = СписокФирм.ПолучитьЗначение(СписокФирм.ТекущаяСтрока());
		ПриИзмененииФирмы();
	КонецЕсли;
КонецПроцедуры // ПриИзмененииСФ

//=========================== ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ =============================

Процедура ОбработкаПодбора(Элемент, КонтФормы)
	Если Элемент.Вид()="ВСД_Площадка" Тогда
		СтараяПлощадка = Элемент.Контрагент;
		
		Если СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Грузополучатель")= СтараяПлощадка Тогда 
			КонтФормы.Форма.Закрыть();    
			Возврат;
		КонецЕсли;
		
		СпрПлощадка = СоздатьОбъект("Справочник.ВСД_Площадка");
		Если (ПустоеЗначение(СтараяПлощадка)=0) И 
			(СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Грузополучатель")<> СтараяПлощадка) Тогда 
			Если Вопрос("Выбранная площадка закреплена за "+Элемент.Контрагент+" Скопировать площадку?",4,30)=6 Тогда 
				СпрПлощадка.Новый();
				СпрПлощадка.Наименование = Элемент.Наименование;
				СпрПлощадка.Контрагент = СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Грузополучатель");
				СпрПлощадка.GUID = Элемент.GUID;
				СпрПлощадка.UUID = Элемент.UUID;
				СпрПлощадка.Адрес = Элемент.Адрес;
				СпрПлощадка.GuidХозСубъекта = Элемент.GuidХозСубъекта;
				СпрПлощадка.Склад = Элемент.Склад;
				СпрПлощадка.НомерПлощадки = Элемент.НомерПлощадки;
				СпрПлощадка.Записать();
				
			Иначе
				//замена контрагента в площадке			
				
				Если СпрПлощадка.ВыбратьЭлементыПоРеквизиту("Контрагент",СтараяПлощадка,0,0)=1 Тогда
					Пока СпрПлощадка.ПолучитьЭлемент()=1 Цикл
						//Сообщить("Очистили привязку площадки "+СпрПлощадка.Контрагент);
						СпрПлощадка.Контрагент = "";
						СпрПлощадка.Записать();				
					КонецЦикла;
				КонецЕсли;
				СпрПлощадка.НайтиЭлемент(Элемент);
				СпрПлощадка.Контрагент = СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Грузополучатель");
				СпрПлощадка.Записать();
				
			КонецЕсли;	
		Иначе
			СпрПлощадка.НайтиЭлемент(Элемент);
			СпрПлощадка.Контрагент = СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Грузополучатель");
			СпрПлощадка.Записать();			
		КонецЕсли;
		КонтФормы.Форма.Закрыть();    
		
		СписокДокументов.УстановитьЗначение(ВыбСтрока,"Площадка", СпрПлощадка.ТекущийЭлемент());
				
		//РаскраситьСписокДокументов( );		
		ЗаполнитьСписокДокументов_Реализации();
	ИначеЕсли Элемент.Вид()="ВСД_Партия" Тогда
		КонтФормы.Форма.Закрыть();    
		
		СписокПартий.УстановитьЗначение(ВыбСтрока,"Партия", Элемент);
		СписокПартий.УстановитьЗначение(ВыбСтрока,"Колво", Элемент.Количество);
		Попытка СписокПартий.УстановитьЗначение(ВыбСтрока,"КолвоМест", Элемент.КоличествоМест); Исключение КонецПопытки;				
		
		РаскраситьСписокПартий();				

	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииРазмераОкна(ТипСобытия, НовШирина, НовВысота) Экспорт
	ГМ._ПриИзмененииРазмераОкна(Контекст, ТипСобытия, НовШирина, НовВысота);
КонецПроцедуры

//_____________________________________________________________________________
Процедура ПослеОткрытия()
	ГМ._ПослеОткрытия(Контекст);
КонецПроцедуры

Процедура ПриОткрытии()
// Дмитрий добавлено
	ИмяФайла="";
	КаталогОбработки="";
	
	РасположениеФайла(КаталогОбработки, ИмяФайла);
//*************

	ВыбФирма = "";
    ГМ = СоздатьОбъект("Меркурий_ГлобальныйМодуль");
	//Если ПустоеЗначение(Отправитель_ХозСубъект) = 0 Тогда
	//	ВыбФирма = ГМ.ПолучитьФирмуПоХС(Отправитель_ХозСубъект);
	//КонецЕсли;
	//ГМ.Инициализация(Контекст);        
    //ГМ.ЗагрузитьПараметрыВФорму(Контекст);

	ГМ._ПриОткрытии(Контекст);
	
	// Глобальный модуль Ветис.2.0
	ГМ2 = СоздатьОбъект("Меркурий_ГлобальныйМодуль2");
	ГМ2.Инициализация(ГМ);	
	
	Автор = ГМ.ПолучитьАвтора();	
	попытка
		Филиал = Автор.Филиал;
	Исключение
		Филиал="";
	КонецПопытки;	
	
	ЗаголовокНадпись();   
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Реализации");
	Форма.Закладки.ДобавитьЗначение("Партии");
	Форма.Закладки.ДобавитьЗначение("Параметры");
	          
	Форма.ИспользоватьСлой("Основной, СписокДокументов");
	
	Партия = "";
	
	//{ Обработку можно вызвать для заполнения списка реализаций на основании маршрута
	// для этого в Маршрут в печатные формы добавьте ПечФорма_ВСД_ГрупповаяОбработка.ert
	//
	Парам = Форма.Параметр;
	Если ТипЗначенияСтр(Парам)="СписокЗначений" Тогда  
				
		Если ПустоеЗначение(Парам)=0 тогда 
			Для Д=1 По Парам.РазмерСписка() Цикл
				Док = Парам.ПолучитьЗначение(Д);
				СписокДокументов.НоваяСтрока();
				СписокДокументов.Пометка= 1;
				//СписокДокументов.инфо = ;
				СписокДокументов.Док = Док;
				//СписокДокументов.ВСД = ;
				СписокДокументов.Колво = Док.ВесДокумента;
				Попытка СписокДокументов.КолвоМест = Док.КоличествоМестДокумента; Исключение КонецПопытки;					
				СписокДокументов.Контрагент = Док.Контрагент;
				СписокДокументов.Грузополучатель = Док.ПолучитьАтрибут(НазваниеРеквизитаГрузополучатель);
				СписокДокументов.ХозСубъект = ГМ.НайтиХозСубъект(СписокДокументов.Контрагент);
				СписокДокументов.Площадка = ГМ.НайтиПлощадкуПоКонтрагенту(СписокДокументов.Грузополучатель);
			КонецЦикла;
			
			ТПДокументов.ОбновитьСтроки();
			
			ВыделитьВсеДокументы();	
		КонецЕсли;
	Иначе
		//стандартное заполнение документов
		ЗаполнитьСписокДокументов_Реализации();
	КонецЕсли;   
	//}
	
	
	Если ПустоеЗначение(ДобавлятьУпаковки)=1 Тогда 
		
		СписокДокументов.УдалитьКолонку("КолвоМест");
		СписокПартий.УдалитьКолонку("КолвоМест");
		СписокПартий.УдалитьКолонку("КолвоМестСписания");
		
	КонецЕсли;

	Если Форма.МодальныйРежим() = 0 Тогда
		оПривязки.Привязка("ТПДокументов", "H", "Форма", "W", "Форма");
		оПривязки.Привязка("СписокПартий", "H", "Форма", "W", "Форма");
	КонецЕсли;
	
КонецПроцедуры

//======================================================================
Процедура ПослеСозданияФормы()
	ТПДокументов = ГМ.СоздатьТабличноеПоле(Контекст, "ТПДокументов", СписокДокументов,,1);
КонецПроцедуры

 // предопределенная процедура
 Процедура ПриВыбореЗакладки(НомерЗакладки, ЗначениеЗакладки)
	 	
 	Если ЗначениеЗакладки="Реализации" Тогда
		Форма.ИспользоватьСлой("Основной, СписокДокументов");
		//СписокДокументов.УдалитьСтроки();
		Если СписокДокументов.КоличествоСтрок()=0 Тогда
			ЗаполнитьСписокДокументов_Реализации();
		КонецЕсли;		
	ИначеЕсли ЗначениеЗакладки="Партии" Тогда
		Форма.ИспользоватьСлой("Основной, Партии");
		//ЗаполнитьПартии();
	Иначе
		Форма.ИспользоватьСлой("Параметры");
	КонецЕсли;     
		
КонецПроцедуры 


//ЖД доп
Процедура ПриИзмененииПлощадки()
	//Обновить список документов 
	ГМ.СписокКонстант.Установить("Отправитель_Площадка",Отправитель_Площадка);	
	
	СписокДокументов.УдалитьСтроки();
	СписокПартий.УдалитьСтроки();		
	ЗаполнитьСписокДокументов_Реализации();
КонецПроцедуры



ТПДокументов = "";
НачДата = ТекущаяДата();
КонДата = ТекущаяДата();

СписокДокументов = СоздатьОбъект("ТаблицаЗначений");
СписокДокументов.НоваяКолонка("сЦвет");
СписокДокументов.НоваяКолонка("Пометка",,,,,5,);
СписокДокументов.НоваяКолонка("Грузополучатель",,,,,20,);
СписокДокументов.НоваяКолонка("Площадка",,,,,20,);
СписокДокументов.НоваяКолонка("ВСД",,,,,10,);
СписокДокументов.НоваяКолонка("Колво","Число",10,3,"Количество",10,);
СписокДокументов.НоваяКолонка("КолвоМест","Число",10,3,"Кол-во мест",10,);
СписокДокументов.НоваяКолонка("Док",,,,"Документ",30,);
СписокДокументов.НоваяКолонка("Контрагент",,,,,15,);
СписокДокументов.НоваяКолонка("ХозСубъект",,,,,15,);

СписокПартий.НоваяКолонка("сЦвет",,,,,1,);
СписокПартий.НоваяКолонка("ВСД_Продукция_Элемент",,,,,20,);
СписокПартий.НоваяКолонка("Партия",,,,,15,);
СписокПартий.НоваяКолонка("Колво","Число",,,,5,);
СписокПартий.НоваяКолонка("КолвоМест","Число",,,,5,);
СписокПартий.НоваяКолонка("КолвоСписания","Число",,,,5,);
СписокПартий.НоваяКолонка("КолвоМестСписания","Число",,,,5,);
СписокПартий.НоваяКолонка("ВСД_Производство",,,,,3,);

ЦвЖелтый 	= "FONT[0]BRUSH[65535]FONT_S[0]BRUSH_S[65535]                       ";
ЦвЗеленый 	= "FONT[0]BRUSH[65280]FONT_S[0]BRUSH_S[65280]                       ";
ЦвКрасный  	= "FONT[0]BRUSH[255]FONT_S[0]BRUSH_S[255]                           " ;
ЦвГолубой	= "FONT[0]BRUSH[13421619]FONT_S[0]BRUSH_S[13421619]                 ";
ЦвФиолетовый= "FONT[0]BRUSH[11665663]FONT_S[0]BRUSH_S[11665663]";

НазваниеВидаДокументаРеализация = "Реализация";
НазваниеРеквизитаКоличество = "Количество";

