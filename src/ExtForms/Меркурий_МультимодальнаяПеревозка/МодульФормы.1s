Перем ТП;
Перем ТаблЧасть;
Перем ГМ;
Перем Конт;

//======================================================================
Процедура УправлениеДоступностью()
	Доступность = ?(ТаблЧасть.КоличествоСтрок() > 0, 1, 0);

	Форма.ВыбХС.Доступность(Доступность);
	Форма.ВыбПлощадка.Доступность(Доступность);
	Форма.Страна.Доступность(Доступность);
	Форма.Регион.Доступность(Доступность);
	Форма.Район.Доступность(Доступность);
	Форма.Город.Доступность(Доступность);
	Форма.Улица.Доступность(Доступность);
	Форма.Дом.Доступность(Доступность);
	Форма.Строение.Доступность(Доступность);
	Форма.Помещение.Доступность(Доступность);
	Форма.ТипыТранспорта.Доступность(Доступность);
	Форма.НомерТС.Доступность(Доступность);
	Форма.номерПрицепа.Доступность(Доступность);
	Форма.номерКонтейнера.Доступность(Доступность);

	Форма.кнЗаписатьАдрес.Доступность(Доступность);
КонецПроцедуры // УправлениеДоступностью

//======================================================================
Процедура УправлениеВидимостью()
	Если ТипыТранспорта.ТекущаяСтрока() = 1 Тогда
		Форма.рамкаНомерТС.Заголовок("Номер автомобиля");
	ИначеЕсли ТипыТранспорта.ТекущаяСтрока() = 2 Тогда
		Форма.рамкаНомерТС.Заголовок("Номер вагона");
	ИначеЕсли ТипыТранспорта.ТекущаяСтрока() = 3 Тогда
		Форма.рамкаНомерТС.Заголовок("Номер авиарейса");
	ИначеЕсли ТипыТранспорта.ТекущаяСтрока() = 4 Тогда
		Форма.рамкаНомерТС.Заголовок("Номер контейнера");
	ИначеЕсли ТипыТранспорта.ТекущаяСтрока() = 5 Тогда
		Форма.рамкаНомерТС.Заголовок("Название судна");
	КонецЕсли;

	Видимость = ?(ТипыТранспорта.ТекущаяСтрока() = 1, 1, 0);
	
	Форма.рамкаПрицеп.Видимость(Видимость);
	Форма.номерПрицепа.Видимость(Видимость);
	Форма.рамкаКонтейнер.Видимость(Видимость);
	Форма.номерКонтейнера.Видимость(Видимость);
КонецПроцедуры // УправлениеВидимостью

//======================================================================
Процедура ТаблПолеПриВыводеСтроки(ТабличноеПоле,ОформлениеСтроки,ДанныеСтроки,ТипРегиона)

	Если ТипРегиона = 3 Тогда
		Если ПустоеЗначение(ДанныеСтроки.ВыбПлощадка) = 1 Тогда
			ОбъектЯчейка=ОформлениеСтроки.Ячейки.Получить("ВыбПлощадка");
			ОбъектЯчейка.Текст = "-=не выбран=-";
			ОбъектЯчейка.ЦветТекста=255;// Красный 
		КонецЕсли;
		ОбъектЯчейка=ОформлениеСтроки.Ячейки.Получить("ТипТранспорта");
		Если Число(ДанныеСтроки.ТипТранспорта) <> 0 Тогда
			Стр = "";
			ТипыТранспорта.ПолучитьЗначение(ДанныеСтроки.ТипТранспорта, Стр);
			ОбъектЯчейка.Текст = Стр;
		КонецЕсли;
		ГМ.ВывестиФлажок(ОформлениеСтроки, ДанныеСтроки, "Перегрузка");
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ТаблПолеПриАктивизацииСтроки(ТабличноеПоле)
	Если ПустоеЗначение(ТабличноеПоле.ТекущиеДанные)=1 Тогда Возврат; КонецЕсли;
	Если ТаблЧасть.КоличествоСтрок()=0 Тогда Возврат; КонецЕсли;
	ТаблЧасть.ТекущаяСтрока(ТабличноеПоле.ТекущиеДанные.НомерСтроки);
	ТаблЧасть.ПолучитьСтрокуПоНомеру(ТабличноеПоле.ТекущиеДанные.НомерСтроки);
	
	ВыбХС = ТаблЧасть.ВыбХС;
	ВыбПлощадка = ТаблЧасть.ВыбПлощадка;
	Страна = ТаблЧасть.Страна;
	Регион = ТаблЧасть.Регион;
	Район = ТаблЧасть.Район;
	Город = ТаблЧасть.Город;
	Улица = ТаблЧасть.Улица;
	Дом = ТаблЧасть.Дом;
	Строение = ТаблЧасть.Строение;
	Помещение = ТаблЧасть.Помещение;

	Поз = ТипыТранспорта.НайтиЗначение(ТаблЧасть.ТипТранспорта);
	Если Поз <> 0 Тогда
		ТипыТранспорта.ТекущаяСтрока(Поз);	
	КонецЕсли;

	НомерТС = ТаблЧасть.НомерТС;
	номерПрицепа = ТаблЧасть.номерПрицепа;
	номерКонтейнера = ТаблЧасть.номерКонтейнера;
	
	Форма.Обновить();
КонецПроцедуры

//======================================================================
Процедура ТаблПолеПриВыбореФлажка(ТабличноеПоле,Стр, Колонка, ТипРегиона)
	ТаблЧасть.Перегрузка = ?(ТаблЧасть.Перегрузка = 1,0,1);
	ТабличноеПоле.ОбновитьСтроки();
КонецПроцедуры
         
//******************************************************************************
// ИзменениеПозиции(НаправлениеСдвига)
//
// Параметры:
//  НаправлениеСдвига = 	-1 - вниз
//							 1 - вверх
// Возвращаемое значение: 
//	Нет
//
// Вызывается из формул элементов диалога:
//  кнопки "вверх" и "вниз" рядом со списком группировок
//
// Описание:  
//	Процедура производит сдвиг текущей строки в общем
// 	списке на "НаправлениеСдвига" позиций
//
Процедура ИзменениеПозиции(НаправлениеСдвига)
	
	Если Число(ТП.ТекущаяСтрока) > 0 Тогда
		ТекСтр = ТП.ТекущаяСтрока;
		ПослСдвигСтр = ТаблЧасть.КоличествоСтрок();
		Если не((НаправлениеСдвига = 1) и (ТекСтр = ПослСдвигСтр)) Тогда
			ТаблЧасть.СдвинутьСтроку(НаправлениеСдвига, ТекСтр);
		КонецЕсли;
		
		ТП.ОбновитьСтроки();
		
		ТекСтр = ТекСтр + НаправлениеСдвига;
		ТекСтр = ?(ТекСтр > ТаблЧасть.КоличествоСтрок(), ТаблЧасть.КоличествоСтрок(), ТекСтр);
		ТекСтр = ?(ТекСтр > 0, ТекСтр, 1);
		Если ТаблЧасть.КоличествоСтрок() > 0 Тогда
			ТП.ТекущаяСтрока = ТекСтр;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ИзменениеПозиции()  

//======================================================================
Процедура ПоКнопкеДобавитьСтроку()
	ТаблЧасть.НоваяСтрока();
	ТП.ТекущаяСтрока = ТаблЧасть.КоличествоСтрок();
	ТаблЧасть.ТипТранспорта = ТипыТранспорта.ПолучитьЗначение(ТипыТранспорта.ТекущаяСтрока());
	ТП.ОбновитьСтроки();

	УправлениеДоступностью();
КонецПроцедуры // ДобавитьСтроку

//======================================================================
Процедура ПоКнопкеУдалитьСтроку()
	Если ПустоеЗначение(ТП.ТекущиеДанные)=1 Тогда Возврат; КонецЕсли;

	ТаблЧасть.УдалитьСтроку(ТП.ТекущаяСтрока);	
	ТП.ОбновитьСтроки();
КонецПроцедуры // ПоКнопкеУдалитьСтроку

//======================================================================
Процедура ПоКнопкеУдалитьВсе()
	ТаблЧасть.УдалитьСтроки();	
	ТП.ОбновитьСтроки();
КонецПроцедуры // ПоКнопкеУдалитьВсе

//======================================================================
Процедура ПоКнопкеЗаписать()
	Если ПустаяСтрока(Конт.ИмяФайлаМультимодального) = 0 Тогда
		ЗначениеВФайл(Конт.ИмяФайлаМультимодального, ТаблЧасть);
	КонецЕсли;
КонецПроцедуры // ПоКнопкеЗаписать

//======================================================================
Процедура ПоКнопкеЗаписатьАдрес()
	Если ВыбПлощадка.Выбран() = 1 Тогда
		СпрПл = СоздатьОбъект("Справочник.ВСД_Площадка");
		СпрПл.НайтиЭлемент(ВыбПлощадка);

		СпрПл.Страна = Страна;
		СпрПл.Регион = Регион;
		СпрПл.Район= Район;
		СпрПл.Город= Город;
		СпрПл.Улица= Улица;
		СпрПл.Дом= Дом;
		СпрПл.Строение= Строение;
		СпрПл.Помещение= Помещение;

		СпрПл.Записать();
	Иначе
		Предупреждение("Не выбрана площадка!");
	КонецЕсли;
КонецПроцедуры // ПоКнопкеЗаписатьАдрес

//======================================================================
Процедура ПоКнопкеЗагрузитьАдрес()
	
КонецПроцедуры // ПоКнопкеЗагрузить

//======================================================================
Процедура ПриВыбореПлощадки()
	Страна = ВыбПлощадка.Страна;	
	Регион = ВыбПлощадка.Регион;	
	Район = ВыбПлощадка.Район;	
	Город = ВыбПлощадка.Город;	
	Улица = ВыбПлощадка.Улица;	
	Дом = ВыбПлощадка.Дом;	
	Строение = ВыбПлощадка.Строение;	
	Помещение = ВыбПлощадка.Помещение;	
	
	ТаблЧасть.ВыбПлощадка = ВыбПлощадка;
	ТаблЧасть.Страна = Страна;
	ТаблЧасть.Регион = Регион;
	ТаблЧасть.Район = Район;
	ТаблЧасть.Город = Город;
	ТаблЧасть.Улица = Улица;
	ТаблЧасть.Дом = Дом;
	ТаблЧасть.Строение = Строение;
	ТаблЧасть.Помещение = Помещение;
	
	ТП.ОбновитьСтроки();
КонецПроцедуры // ПриВыбореПлощадки

//======================================================================
Процедура ПриВыбореТипаТранспорта()
	УправлениеВидимостью();

	ТаблЧасть.ТипТранспорта = ТипыТранспорта.ПолучитьЗначение(ТипыТранспорта.ТекущаяСтрока());
	ТП.ОбновитьСтроки();
КонецПроцедуры // ПриВыбореТипаТранспорта

//======================================================================
Процедура ПриИзмененииРеквизита(ИмяРеквизита, Значение)
	ТаблЧасть.УстановитьЗначение(ТаблЧасть.ТекущаяСтрока(), ИмяРеквизита, Значение);
	ТП.ОбновитьСтроки();
	Если ИмяРеквизита = "НомерТС" Тогда
		ТаблЧасть.номерАвто = "";		ТаблЧасть.номерВагона = "";
		ТаблЧасть.НомерАвиарейса = "";	ТаблЧасть.НазваниеСудна = "";
		Если ТипыТранспорта.ТекущаяСтрока() = 1 Тогда
			ВремРеквизит = "номерАвто";
		ИначеЕсли ТипыТранспорта.ТекущаяСтрока() = 2 Тогда
			ВремРеквизит = "номерВагона";
		ИначеЕсли ТипыТранспорта.ТекущаяСтрока() = 3 Тогда
			ВремРеквизит = "НомерАвиарейса";
		ИначеЕсли ТипыТранспорта.ТекущаяСтрока() >= 4 Тогда
			ВремРеквизит = "НазваниеСудна";
		КонецЕсли;
		ТаблЧасть.УстановитьЗначение(ТаблЧасть.ТекущаяСтрока(), ВремРеквизит, Значение);
	КонецЕсли;
КонецПроцедуры // ПриИзмененииРеквизита("Страна", Страна)

//======================================================================
Процедура ПриОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) <> "СписокЗначений" Тогда
		Сообщить("Данная обработка вызывается из документа Транзакция2!"); 
		СтатусВозврата(0); Возврат;
	КонецЕсли;
	Конт = Форма.Параметр.Получить("Документ");
	ГМ = Форма.Параметр.Получить("ГМ");
	
	ТипыТранспорта.ДобавитьЗначение(1,"Автомобильный");
	ТипыТранспорта.ДобавитьЗначение(2,"Железнодорожный");
	ТипыТранспорта.ДобавитьЗначение(3,"Авиатранспортный");
	ТипыТранспорта.ДобавитьЗначение(4,"Морской (контейнер)");
	ТипыТранспорта.ДобавитьЗначение(5,"Морской (трюм)");
	ТипыТранспорта.ТекущаяСтрока(1);

	ТаблЧасть = СоздатьОбъект("ТаблицаЗначений");
	ТаблЧасть.НоваяКолонка("ВыбХС","Справочник.ВСД_ХозСубъект");
	ТаблЧасть.НоваяКолонка("ВыбПлощадка","Справочник.ВСД_Площадка",,,"Площадка",20);
	ТаблЧасть.НоваяКолонка("Страна","Справочник.ВСД_Страна");
	ТаблЧасть.НоваяКолонка("Регион","Справочник.ВСД_Регион");
	ТаблЧасть.НоваяКолонка("Район","Справочник.ВСД_Район");
	ТаблЧасть.НоваяКолонка("Город","Справочник.ВСД_Город");
	ТаблЧасть.НоваяКолонка("НасПункт",);
	ТаблЧасть.НоваяКолонка("Улица","Справочник.ВСД_Улица");
	ТаблЧасть.НоваяКолонка("Дом","Строка");
	ТаблЧасть.НоваяКолонка("Строение","Строка");
	ТаблЧасть.НоваяКолонка("Помещение","Строка");
	ТаблЧасть.НоваяКолонка("Перегрузка","Число",1,0,"Перегрузка",5);
	ТаблЧасть.НоваяКолонка("ТипТранспорта","Число",2,0,"Тип транспорта",15);
	ТаблЧасть.НоваяКолонка("НомерТС","Строка",50,,"Номер ТС",20);
	ТаблЧасть.НоваяКолонка("номерАвто");
	ТаблЧасть.НоваяКолонка("номерКонтейнера");
	ТаблЧасть.НоваяКолонка("номерВагона");
	ТаблЧасть.НоваяКолонка("номерПрицепа");
	ТаблЧасть.НоваяКолонка("НазваниеСудна");
	ТаблЧасть.НоваяКолонка("НомерАвиарейса");
	ТаблЧасть.НоваяКолонка("UUID","Строка",36);
	
	Если ПустаяСтрока(Конт.ИмяФайлаМультимодального) = 0 Тогда
		ВремТЗ = ЗначениеИзФайла(Конт.ИмяФайлаМультимодального);
		Если ТипЗначенияСтр(ВремТЗ) = "ТаблицаЗначений" Тогда
			Для СЦ = 1 По ВремТЗ.КоличествоСтрок() Цикл
				ТаблЧасть.НоваяСтрока(); 
				Для СК = 1 По ТаблЧасть.КоличествоКолонок() Цикл
					ИмяКолонки = ТаблЧасть.ПолучитьПараметрыКолонки(СК);
					Попытка
						ТаблЧасть.УстановитьЗначение(СЦ, СК, ВремТЗ.ПолучитьЗначение(СЦ, ИмяКолонки));
					Исключение
					КонецПопытки;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии

//======================================================================
Процедура ПослеСозданияФормы()

	ТП = ГМ.СоздатьТабличноеПоле(Контекст, "ТаблПоле", ТаблЧасть,,1);
	
	Если ТаблЧасть.КоличествоСтрок() > 0 Тогда
		ТП.ТекущаяСтрока = 1;
		ТаблПолеПриАктивизацииСтроки(ТП);
	КонецЕсли;
	
	УправлениеВидимостью();
	УправлениеДоступностью();

КонецПроцедуры

//======================================================================
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, ФлагПродолжения)

	Если ЭлементДиалога = "ВыбПлощадка" Тогда
		ВремЭлем = ВыбХС;

		Список = СоздатьОбъект("СписокЗначений");
		Спр = СоздатьОбъект("Справочник.ВСД_Площадка");
		Спр.ВыбратьЭлементыПоРеквизиту("GuidХозСубъекта",ВремЭлем.GUID);
		Пока Спр.ПолучитьЭлемент() = 1 Цикл
			Список.ДобавитьЗначение(Спр.ТекущийЭлемент());
		КонецЦикла;
		ОткрытьФорму("Справочник.ВСД_Площадка",Список);
		ФлагСтандОбр = 0;
	ИначеЕсли ЭлементДиалога = "Регион" Тогда
		ВремЭлем = Страна;
		ОткрытьФорму("Справочник.ВСД_Регион",ВремЭлем);
		ФлагПродолжения = 0;
	ИначеЕсли ЭлементДиалога = "Район" Тогда
		ВремЭлем = Регион;
		ОткрытьФорму("Справочник.ВСД_Район",ВремЭлем);
		ФлагПродолжения = 0;
	ИначеЕсли ЭлементДиалога = "Город" Тогда
		ВремЭлем = Регион;
		ОткрытьФорму("Справочник.ВСД_Город",ВремЭлем);
		ФлагПродолжения = 0;
	ИначеЕсли ЭлементДиалога = "Улица" Тогда
		ВремЭлем = Город;
		ОткрытьФорму("Справочник.ВСД_Улица",ВремЭлем);
		ФлагПродолжения = 0;
	КонецЕсли;

КонецПроцедуры 

