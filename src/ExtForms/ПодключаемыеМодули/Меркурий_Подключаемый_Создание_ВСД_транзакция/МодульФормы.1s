
//*****************************************************************************
Функция ПолучитьЗначениеСвойства(Номенклатура, ВыбВидСвойств)
	
	СпрСвва = СоздатьОбъект("Справочник.СвойстваНоменклатуры");
	СпрСвва.ИспользоватьВладельца(Номенклатура);
	СпрСвва.ВыбратьЭлементыПоРеквизиту("ВидСвойства",ВыбВидСвойств,1,0);
	Если  СпрСвва.ПолучитьЭлемент() = 1 Тогда 
		Возврат СпрСвва.ЗначениеСвойства;
	КонецЕсли;
	
	Сообщить("Товар ["+Номенклатура+"] свойство "+ВыбВидСвойств+" не указано!","!");	
	Возврат "";
КонецФункции

Функция ПолучитьКоличествоМест(ДокОснование, ВыбТипПродукции, ВыбВидСвойств)
	Результат = 0;
	ДокОснование.ВыбратьСтроки();
	Пока ДокОснование.ПолучитьСтроку()=1 Цикл
		Если ПустоеЗначение(ВыбТипПродукции)=0 Тогда 
			ТекЗнСвойства = ПолучитьЗначениеСвойства(ДокОснование.Номенклатура, ВыбВидСвойств);

			Если ВыбТипПродукции <> ТекЗнСвойства тогда 
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Попытка			
			к = ДокОснование.Количество* ДокОснование.Коэффициент / ДокОснование.Номенклатура.ОсновнаяЕдиница.Коэффициент;
		Исключение
			к =0;
		КонецПопытки;

		Результат=Результат+к;		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция СвернутьТчПоСвойству(Док, ВыбВидСвойств)
	тзДок = СоздатьОбъект("ТаблицаЗначений");
	Док.ВыгрузитьТабличнуюЧасть(тзДок);
	
	тзРез = СоздатьОбъект("ТаблицаЗначений");
	тзРез.НоваяКолонка("Свойство");
	тзРез.НоваяКолонка("Количество");
	тзРез.НоваяКолонка("КоличествоМест");
	
	тзДок.ВыбратьСтроки();
	Пока тзДок.ПолучитьСтроку() = 1 Цикл
		тзРез.НоваяСтрока();
		тзРез.Свойство = ПолучитьЗначениеСвойства(тзДок.Номенклатура, ВыбВидСвойств);
		тзРез.Количество = тзДок.Количество;
		тзРез.КоличествоМест = тзДок.ПолучитьЗначение(тзДок.НомерСтроки, "КоличествоМест" );
	КонецЦикла;
		
	тзРез.Свернуть("Свойство","Количество, КоличествоМест");
	
	Возврат тзРез;
КонецФункции	

Функция СоздатьВСД(Парам)

	ДокОснование 				= Парам.Получить("ДокОснование");
	Контрагент 					= Парам.Получить("Контрагент");
	Грузополучатель 			= Парам.Получить("Грузополучатель");
	Отправитель_ХозСубъект 		= Парам.Получить("Отправитель_ХозСубъект");
	Отправитель_Площадка 		= Парам.Получить("Отправитель_Площадка");
	Перевозчик_ХозСубъект 		= Парам.Получить("Перевозчик_ХозСубъект");
	Партия 						= Парам.Получить("Партия");
	ТермическиеУсловияПеревозки = Парам.Получить("ТермическиеУсловияПеревозки");
	ХС 							= Парам.Получить("ХС");
	Площадка 					= Парам.Получить("Площадка"); 
	Колво 						= Парам.Получить("Колво"); 
	КолвоМест 					= Парам.Получить("КолвоМест"); 
	ВыбТипПродукции 			= Парам.Получить("ВыбТипПродукции"); 
	ВыбВидСвойств 				= Парам.Получить("ВыбВидСвойств"); 
	ВСД_Экспертиза 				= Парам.Получить("ВСД_Экспертиза");    
	ВСД_Местность				= Парам.Получить("ВСД_Местность");
	ВСД_ОсобыеОтметки 			= Парам.Получить("ВСД_ОсобыеОтметки");
	СписокПартий 				= Парам.Получить("СписокПартий");    
		
	ДокВСД = СоздатьОбъект("Документ.ВСД_транзакция");
	ДокВСД.Новый();
	//ДокВСД.НомерДок = ;
	ДокВСД.ДокОснование = ДокОснование;
	
	//ДокВСД.ДатаДок = ДокВСД.ДокОснование.ДатаДок;
	ДокВСД.ДатаДок = ДокОснование.ДатаДок;
	
	ДокВСД.Отправитель_ХозСубъект = Отправитель_ХозСубъект;
	ДокВСД.Отправитель_Площадка = Отправитель_Площадка;
	
	ДокВСД.Получатель_ХозСубъект = ХС;		
	ДокВСД.Получатель_Площадка = Площадка;					
	ДокВСД.Перевозчик_ХозСубъект = Перевозчик_ХозСубъект;

	//ДокВСД.Производитель_Площадка = Партия.Производитель_Площадка;
	
	ТТН = ДокВСД.ДокОснование.Маршрут;		
	//ТТН = СписокДокументов.Док.НомерМаршрута;	
				
	ДокВСД.ТтнСерия = ""; //ТТН.Серия;
	ДокВСД.ТтнНомер = ДокВСД.ДокОснование.НомерДок;
	ДокВСД.ТтнДата = ДокВСД.ДокОснование.ДатаДок;
	ДокВСД.номерАвто = ТТН.Авто.НомернойЗнак;
	Если ПустоеЗначение(ДокВСД.номерАвто)=1 Тогда
		ДокВСД.номерАвто = "не используется";
	КонецЕсли;
	
	ДокВСД.Автор = глПользователь;
	Попытка				
		ДокВСД.Филиал = глПользователь.Филиал;
	Исключение
	КонецПопытки;
	ДокВСД.ФормаВСД = 1;
	ДокВСД.ТермическоеСостояние = ТермическиеУсловияПеревозки;
	
	ДокВСД.Экспертиза 		= ВСД_Экспертиза;
	ДокВСД.Местность 		= ВСД_Местность;
	ДокВСД.ОсобыеОтметки 	= ВСД_ОсобыеОтметки;
	ДокВСД.cargoExpertized  = 1;
	
	тз = СвернутьТчПоСвойству(ДокВСД.ДокОснование, ВыбВидСвойств);
	
	тз.ВыбратьСтроки();
	Пока тз.ПолучитьСтроку() = 1 Цикл
		ДокВСД.НоваяСтрока();
		
		стр=0;
		
		Если СписокПартий.НайтиЗначение(тз.Свойство,стр,"Свойство")>0 Тогда 
			ДокВСД.Партия = СписокПартий.ПолучитьЗначение(стр,"Партия");
		Иначе
			Сообщить("Не найдена партия для свойства "+тз.Свойство+"
			|документ "+ДокВСД.ДокОснование+" пропущен","!");
			Продолжить;
		КонецЕсли;
		ДокВСД.Количество = тз.Количество;
		ДокВСД.КоличествоКор = тз.КоличествоМест;			
		ДокВСД.ЕдиницаИзмерения = ДокВСД.Партия.ЕдиницаИзмерения;
		ДокВСД.ФормаУпаковки = ДокВСД.Партия.ФормаУпаковки;
		ДокВСД.Продукция = ДокВСД.Партия.Продукция;
		ДокВСД.ВидПродукции = ДокВСД.Партия.ВидПродукции;
	КонецЦикла;
	
	//ДокВСД.СвойствоНоменклатурыЗначение = ВыбТипПродукции;
	
	//Если ПустоеЗначение( СокрЛП(ДокВСД.номерАвто) )=1 Тогда
	//	Сообщить("В документе "+ДокВСД.ТтнНомер+"не указан № автомобиля 
	//	|ВСД не сформирован");
	//	Продолжить;
	//КонецЕсли;
	
	ДокВСД.Записать();
			
	Возврат ДокВСД.ТекущийДокумент();
	
КонецФункции

Процедура ПриОткрытии()

	Парам = Форма.Параметр;
	Если ТипЗначенияСтр(Парам)="СписокЗначений" Тогда  
		
		Если ПустоеЗначение(Парам.Получить("ДокОснование"))=1 Тогда
			Сообщить("В форму служебной обработки передан неверный параметр!","I");
			СтатусВозврата(0);Возврат;
		КонецЕсли;      
        
		//Сообщение = Парам.Получить("Сообщение");
                                 
		ДокументСсылка = СоздатьВСД(Парам);
		
		СписокПараметров = СоздатьОбъект("СписокЗначений");
		СписокПараметров.ДобавитьЗначение(ДокументСсылка,"ДокументСсылка");
		                    
		Форма.Параметр = СписокПараметров;   

	КонецЕсли;
    
	СтатусВозврата(0); Возврат;
	
КонецПроцедуры