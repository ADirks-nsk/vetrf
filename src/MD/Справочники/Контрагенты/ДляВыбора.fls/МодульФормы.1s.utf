////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем ФормаТаблицыОстатков;  // контекст формы с таблицей остатков

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// ОсновноеСвойство()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Значение основного свойства
//
// Вызывается из формул реквизитов диалога
//  Колонка "Основное свойство".
//
// Описание:
//  Формирует и возращает строковое представление основного свойства.
//
Функция ОсновноеСвойство()
	
	Если ЭтоГруппа() = 1 Тогда
		Возврат "";
	КонецЕсли;
	
	Если ПустоеЗначение(ТекущийЭлемент()) = 1  Тогда
		Возврат "";
	Иначе  
		Если ПустоеЗначение(ТекущийЭлемент().ОсновноеСвойство) = 1  Тогда
			Возврат "";
		Иначе
			Возврат ТекущийЭлемент().ОсновноеСвойство.ЗначениеСвойства;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции // ОсновноеСвойство()

//******************************************************************************
// ПоКнопкеДолги()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка "Долги".
//
// Описание:
//  Вызывает форму показа остатков по текущему ТМЦ в таблице значений.
//
Процедура ПоКнопкеДолги()
	
	Параметр = глВзятьКонтекст(Контекст);
	ОткрытьФорму("Обработка.ПоказатьОстаткиВзаиморасчетов", Параметр);
	ФормаТаблицыОстатков = Параметр; // через параметр возвращается контекст открытой формы обработки
	
КонецПроцедуры // ПоКнопкеДолги()

//******************************************************************************
// СформироватьИнформационнуюСтроку()
//
// Параметры:
//  Нет
//
//	Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Текст информационной строки.
//
// Описание:
//  Формирует текст информационной строки формы. 
//
Функция СформироватьИнформационнуюСтроку()
	Перем ИнфСтрока;
	
	Если ПустоеЗначение(ТекущийЭлемент()) = 1 Тогда
		// не выбран элемент
		ИнфСтрока = "";
	Иначе
		
		Если ТекущийЭлемент().ЭтоГруппа() = 1 Тогда
			// текущий элемент - группа
			ИнфСтрока =  ТекущийЭлемент().ПолноеНаименование();
		Иначе
			
			ТекЭлем = ТекущийЭлемент();
			
			Если ТекЭлем.ЮрФизЛицо.Выбран() = 1 Тогда
				ИнфСтрока = СокрЛП(ТекЭлем.Наименование) +
				            ?(ПустоеЗначение(ТекЭлем.ЮрФизЛицо.Телефоны) = 0, "  Телефоны: "     + СокрЛП(ТекЭлем.ЮрФизЛицо.Телефоны), "") +
				            ?(ПустоеЗначение(ТекЭлем.ЭлПочта) = 0, "  Адрес эл.почты: " + СокрЛП(ТекЭлем.ЭлПочта),	"") +
							?(ПустоеЗначение(ТекЭлем.Комментарий) = 0, "  Комментарий: "    + СокрЛП(ТекЭлем.Комментарий),	"");

			КонецЕсли;			
		
			Если ТипЗначения(ФормаТаблицыОстатков) = 100 Тогда
				
				// если открыта форма с таблицей остатков, то обновим в ней текущий элемент
				ФормаТаблицыОстатков.Форма.Обновить(0);
			    
			КонецЕсли;			
		КонецЕсли;

	КонецЕсли;
	
	Возврат ИнфСтрока;
	
КонецФункции // СформироватьИнформационнуюСтроку()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	Если ПустоеЗначение(Константа.ВидСвойстваКонтрагентов) = 0  Тогда
		Форма.Свойство.Заголовок(Строка(Константа.ВидСвойстваКонтрагентов));
	Иначе
		Форма.Свойство.Видимость(0);
	КонецЕсли;
	
	Если Форма.Параметр = "Выбор"  Тогда
	    Форма.ОбработкаВыбораСтроки(1);
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗакрытии()
	
	// если открыта форма с таблицей остатков, то закроем и ее
	Если ТипЗначения(ФормаТаблицыОстатков) = 100 Тогда
		ФормаТаблицыОстатков.Форма.Закрыть(0);
	КонецЕсли;			
	
КонецПроцедуры // ПриЗакрытии()         

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриВыбореСтроки()
	Форма.Параметр = ТекущийЭлемент();
	Возврат;
КонецПроцедуры
