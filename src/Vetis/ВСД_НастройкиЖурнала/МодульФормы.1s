//*******************************************
Процедура Сформировать()
	СписокДокументов = СоздатьОбъект("СписокЗначений");
	Для СЦ = 1 По спВидДокумента.РазмерСписка() Цикл
		Если спВидДокумента.Пометка(СЦ) = 1 Тогда
			СписокДокументов.ДобавитьЗначение(спВидДокумента.ПолучитьЗначение(СЦ));
		КонецЕсли;
	КонецЦикла;
	СохранитьЗначение("ВСД_НастройкиЖурнала", СписокДокументов);		
КонецПроцедуры

//******************************************************************************
// ГрупповаяПометка(Режим)
//
// Параметры: 
//  Режим:
//   1 - пометить все
//   2 - снять пометку
//   3 - инвертировать пометку
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
// кнопки групповой пометки
//
// Описание:
//  Производит групповые действия с пометкой строк таблицы формы
//
Процедура ГрупповаяПометка(Режим)

	Для СЦ = 1 По спВидДокумента.РазмерСписка() Цикл
		Если Режим=1 Тогда              
			спВидДокумента.Пометка(СЦ, 1);
		ИначеЕсли Режим=2 Тогда
			спВидДокумента.Пометка(СЦ, 0);
		ИначеЕсли Режим=3 Тогда         
			спВидДокумента.Пометка(СЦ, ?(спВидДокумента.Пометка(СЦ)=1,0,1));
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ГрупповаяПометка()         

//======================================================================
Процедура ПриОткрытии()
	Для Индекс = 1 По Метаданные.Документ() Цикл
		Док = Метаданные.Документ(Индекс);
		Значение = Док.Идентификатор;
		Если Лев(Значение, 3) = "ВСД" Тогда
			Представление = Док.Представление();
			Комментарий = Док.Комментарий;
			Если ПустаяСтрока(Комментарий) = 0 Тогда
				Представление = Представление + " (" + Комментарий +")";
			КонецЕсли;
			спВидДокумента.ДобавитьЗначение(Значение, Представление);
		КонецЕсли;
	КонецЦикла;
	
	СписокДокументов = ВосстановитьЗначение("ВСД_НастройкиЖурнала");
	Если ПустоеЗначение(СписокДокументов) = 1 Тогда
		СписокДокументов = СоздатьОбъект("СписокЗначений");
		СписокДокументов.ДобавитьЗначение("ВСД_транзакция");
		СписокДокументов.ДобавитьЗначение("ВСД2_транзакция");
		СписокДокументов.ДобавитьЗначение("ВСД_Инвентаризация");
		СписокДокументов.ДобавитьЗначение("ВСД2_Инвентаризация");
		
		СохранитьЗначение("ВСД_НастройкиЖурнала", СписокДокументов);		
	КонецЕсли;
	
	Для СЦ = 1 По СписокДокументов.РазмерСписка() Цикл
		ВидДокум = СписокДокументов.ПолучитьЗначение(СЦ); 
		Поз = спВидДокумента.НайтиЗначение(ВидДокум);
		Если Поз <> 0 Тогда
			спВидДокумента.Пометка(Поз, 1);
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры // ПриОткрытии