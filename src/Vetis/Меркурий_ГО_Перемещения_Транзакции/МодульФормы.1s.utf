// Автор: Синявский Филипп phsin@kb99.pro
// Все права принадлежат ИП Синявский Ф.А.

//Перем ГМ;
//Перем ГМ2; // Глобальный модуль для Ветис 2.0
Перем цвКрасный, цвЖелтый, цвЗеленый, цвГолубой, цвФиолетовый;
Перем ВыбСтрока;
Перем СписокПартий;

Перем оПривязки; //:Меркурий.Привязки
Перем ТПДокументов, СписокДокументов;

Перем ВыбФирма Экспорт;


Процедура ПриНачалеВыбораЗначения(ЭлементДиалога,ФлагСтандОбр)
	Если ЭлементДиалога = "Отправитель_Площадка" Тогда
		ВремЭлем = Отправитель_ХозСубъект;
		ОткрытьФорму("Справочник.ВСД_Площадка",ВремЭлем);
		ФлагСтандОбр = 0;		
	КонецЕсли;
КонецПроцедуры // ПриНачалеВыбораЗначения()

//======================================================================
Процедура УменьшитьНаРаспределенныеПартии(ТзАктуальныхПартий)
	// Подберем уже заполненнные, но не отправленные ВСД, 
	// Выбираем ВСЕ документы, т.к. они м.б. не отмечены, но в них есть эти партии к отправке!!!
	тз = СоздатьОбъект("Таблицазначений");
	тзДокстр = СоздатьОбъект("Таблицазначений");
	СписокДокументов.Выгрузить(тз);
	тз.Свернуть("ВСД","");
	//	тз.Сортировать("ВСД");
	тз.ВыбратьСтроки();
	Пока тз.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(тз.ВСД) = 1 Тогда
		    Продолжить;
		КонецЕсли;
		Если (тз.ВСД.Проведен() = 1) или (СокрЛП(тз.ВСД.Статус) = "COMPLETED") Тогда
		    Продолжить;
		КонецЕсли;
		
		тз.ВСД.ВыгрузитьТабличнуюЧасть(тзДокстр);
		тзДокСтр.Свернуть("Партия","Количество");
		тзДокСтр.ВыбратьСтроки();
		Пока тзДокСтр.ПолучитьСтроку() = 1 Цикл
		    Если ПустоеЗначение(тздокСтр.Партия) = 1 Тогда
		        Продолжить;
			КонецЕсли;
			стр = 0;
			Если ТзАктуальныхПартий.НайтиЗначение(тздокСтр.Партия,стр,"ВСД_Партия") = 1 Тогда
				ТзАктуальныхПартий.ПолучитьСтрокуПоНомеру(стр);
				ТзАктуальныхПартий.Количество = ТзАктуальныхПартий.Количество - тздокСтр.Количество;
				Если ТзАктуальныхПартий.Количество < 0  Тогда
					Сообщить("В заполненных ранее ВСД на отправку обнаружено ПРЕВЫШЕНИЕ количества имеющейся партии (№ записи : "+СокрЛП(ТзАктуальныхПартий.ВСД_Партия.НомерЗаписи)+") ;"+тз.ВСД,"!!");
					ТзАктуальныхПартий.Количество = 0;    
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	//Совсем выкинем уже распределенные Партии из списка Актуальных
	стр = 0;
	Пока ТзАктуальныхПартий.НайтиЗначение(0,стр,"Количество") = 1 Цикл
		ТзАктуальныхПартий.УдалитьСтроку(стр);
		стр = 0;
	КонецЦикла;
КонецПроцедуры

//****** Распределение партий - Исключительно вывод на форму

//======================================================================
Процедура РаскраситьСписокПартий2()
	тзНеобходимыеПартии.ВыбратьСтроки();
	Пока тзНеобходимыеПартии.ПолучитьСтроку() = 1 Цикл
		Если _Подробно = 0 Тогда
 			Если (тзНеобходимыеПартии.Колво = 0 ) Тогда 
				тзНеобходимыеПартии.сЦвет = цвКрасный;	
			ИначеЕсли (тзНеобходимыеПартии.Колво >= тзНеобходимыеПартии.КолвоСписания) Тогда
				тзНеобходимыеПартии.сЦвет = цвЗеленый;
			Иначе			
				тзНеобходимыеПартии.сЦвет = цвЖелтый;							
			КонецЕсли;
		Иначе
 			Если (тзНеобходимыеПартии.Колво > 0 ) и ( ПустоеЗначение(тзНеобходимыеПартии.Партия)=0 ) Тогда 
				тзНеобходимыеПартии.сЦвет = цвЗеленый;	
			Иначе			
				тзНеобходимыеПартии.сЦвет = цвКрасный;							
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;
КонецПроцедуры

//======================================================================
Процедура ВывестиТзПартийВариант2()
	ПарамКолонкаСортировкиПартииСписания = ГМ.ПолучитьКонстанту("ПарамКолонкаСортировкиПартииСписания");
	ПарамЗнакСортировкиУбывание = ГМ.ПолучитьКонстанту("ПарамЗнакСортировкиУбывание");
	ПарамЗаполнятьВСДБезПартий = ГМ.ПолучитьКонстанту("ПарамЗаполнятьВСДБезПартий");

	тзНеобходимыеПартии.УдалитьСтроки();
    // Используем список Продукция_Элемент, полученный ф-цией ЗаполнитьПартии
	СзПродукцияЭлементы = СоздатьОбъект("СписокЗначений"); // Для заполнения свободных партий
	ТзПродукцияЭлементы = СоздатьОбъект("ТаблицаЗначений");
	СписокПартий.Выгрузить(ТзПродукцияЭлементы);
	
	ТзПродукцияЭлементы.Свернуть("ВСД_Продукция_Элемент","Колво,КолвоСписания"); //Вывод итогов по прод элементу
	ТзПродукцияЭлементы.Выгрузить(СзПродукцияЭлементы,,,"ВСД_Продукция_Элемент");

	// Получим нужные нам Актуальные партии по фильтру
	ВремТЗПодобранныеПартии = ГМ.ПолучитьАкуальныеПартииИзСправочника(СзПродукцияЭлементы,Отправитель_Площадка,Отправитель_ХозСубъект,СокрЛП(ПарамКолонкаСортировкиПартииСписания),ПарамЗнакСортировкиУбывание);
    //ВремТЗПодобранныеПартии.ВыбратьСтроку(); //ТЕСТ
	Если ПустоеЗначение(ВремТЗПодобранныеПартии) = 1 Тогда
		Если ПустоеЗначение(ВремТЗПодобранныеПартии) = 1 Тогда
			Сообщить("Нет актуальных партий по данным условиям отбора");
			Возврат;
		КонецЕсли;
	КонецЕсли;

	УменьшитьНаРаспределенныеПартии(ВремТЗПодобранныеПартии);
	
	ТЗРаспределения = СоздатьОбъект("ТаблицаЗначений"); // Итоговая таблица

	Если _Подробно = 0 Тогда
		ТзПродукцияЭлементы.Выгрузить(ТЗРаспределения);
		ТЗРаспределения.НоваяКолонка("ВСД_Производство",,,,,3,);
		ВремПартии = СоздатьОбъект("ТаблицаЗначений");
		ВремТЗПодобранныеПартии.Выгрузить(ВремПартии);
		ВремПартии.Свернуть("Продукция_Элемент","Количество,КоличествоМест"); //Актуальное кол-во к списанию по Прод_Элементу
		ТЗРаспределения.ВыбратьСтроки();		
		Пока ТЗРаспределения.ПолучитьСтроку() = 1 Цикл
			стр = 0;
			Если ВремПартии.НайтиЗначение(ТЗРаспределения.ВСД_Продукция_Элемент,стр,"Продукция_Элемент") = 1 Тогда
			    ВремПартии.ПолучитьСтрокуПоНомеру(стр);
				ТЗРаспределения.Колво = ВремПартии.Количество;
			КонецЕсли;
		КонецЦикла;
		ТзПродукцияЭлементы = "";
		ВремПартии = "";
	Иначе
		//Распределим по Актуальным
		СписокПартий.Выгрузить(ТЗРаспределения); //скопируем стркуктуру
		ТЗРаспределения.УдалитьСтроки();

		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			Если СписокДокументов.Пометка<>1 Тогда
				Продолжить;
			КонецЕсли;

			Если (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда       
				Если (ПустоеЗначение(СписокДокументов.ВСД.Статус) = 1) Тогда 
					Сообщить("Для "+СписокДокументов.Док+" ВСД уже создан, но не отправлен","i");
					Продолжить;
				КонецЕсли;		
			КонецЕсли;

			тз = ГМ.ЗаполнитьПартииПоТЧДокумента2(СписокДокументов.Док,Отправитель_Площадка, Отправитель_ХозСубъект, ВремТЗПодобранныеПартии,ПарамЗаполнятьВСДБезПартий);//,СокрЛП(ПарамКолонкаСортировкиПартииСписания),ПарамЗнакСортировки);
			тз.ВыбратьСтроки();
			Пока тз.ПолучитьСтроку() = 1 Цикл
				ТЗРаспределения.НоваяСтрока();
				ТЗРаспределения.ДокРеализации = СписокДокументов.Док;
				ТЗРаспределения.Номенклатура = тз.Номенклатура;
				ТЗРаспределения.ВСД_Продукция_Элемент = тз.Продукция_Элемент;
				ТЗРаспределения.Партия = тз.Партия;
				ТЗРаспределения.ПолеСортировки = тз.ПолеСортировки;
				ТЗРаспределения.Колво = ?(ПустоеЗначение(ТЗРаспределения.Партия) = 1,0,тз.Количество);
				ТЗРаспределения.КолвоСписания = тз.Количество;
			КонецЦикла;
        
		КонецЦикла;
		
		Если ПустоеЗначение(ПарамКолонкаСортировкиПартииСписания) = 0 Тогда
			ТЗРаспределения.УстановитьПараметрыКолонки("ПолеСортировки",,,,СокрЛП(ПарамКолонкаСортировкиПартииСписания));
		КонецЕсли;
		
	КонецЕсли;
	
	ТЗРаспределения.ВставитьКолонку("сЦвет",1,,,,,1);
	ТЗРаспределения.Выгрузить(тзНеобходимыеПартии);

	РаскраситьСписокПартий2();
КонецПроцедуры

//======================================================================
Процедура КликПоказатьСвернуто()
	ВывестиТзПартийВариант2();	
КонецПроцедуры
//****** Распределение партий - вывод на форму Окончание

//=========================== СЛУЖЕБНЫЕ ФУНКЦИИ ========================

//======================================================================
Процедура ОбновитьИнф()
	
	Форма.Инфо.Заголовок("Документов: "+СписокДокументов.КоличествоСтрок()+", количество: "+Окр(СписокДокументов.Итог("Колво"))+" ");
	
	ВыбКолво =0;
	ВыбДок = 0;
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка = 1 Тогда 
			ВыбКолво = ВыбКолво + (СписокДокументов.Колво);
			ВыбДок=ВыбДок+1;
		КонецЕсли;
	КонецЦикла;	      
	ВыбКолво = Окр(ВыбКолво);

	Форма.ИнфоВыбор.Заголовок("Выбрано "+ВыбДок+" документов, количество = "+ВыбКолво+" ");
	
КонецПроцедуры

//======================================================================
Процедура ЗаголовокНадпись()
	
	форма.НачалоПериодаТекст.Заголовок(СокрЛП(НачДата));
	форма.КонецПериодаТекст.Заголовок(сокрлп(КонДата));

КонецПроцедуры // ЗаголовокНадпись

//======================================================================
Процедура РаскраситьтзНеобходимыеПартии()
	тзНеобходимыеПартии.ВыбратьСтроки();
	Пока тзНеобходимыеПартии.ПолучитьСтроку() = 1 Цикл		

		Если ДобавлятьУпаковки = 1 Тогда 		
		
			Если (тзНеобходимыеПартии.Колво = 0 ) или (тзНеобходимыеПартии.КолвоМест=0) Тогда 
				тзНеобходимыеПартии.сЦвет = цвКрасный;	
			ИначеЕсли (тзНеобходимыеПартии.Колво >= тзНеобходимыеПартии.КолвоСписания) и 
				(тзНеобходимыеПартии.КолвоМест >= тзНеобходимыеПартии.КолвоМестСписания) Тогда
					тзНеобходимыеПартии.сЦвет = цвЗеленый;
			Иначе			
				тзНеобходимыеПартии.сЦвет = цвЖелтый;							
			КонецЕсли;
		Иначе
			Если (тзНеобходимыеПартии.Колво = 0 ) Тогда 
				тзНеобходимыеПартии.сЦвет = цвКрасный;	
			ИначеЕсли (тзНеобходимыеПартии.Колво >= тзНеобходимыеПартии.КолвоСписания) Тогда
				тзНеобходимыеПартии.сЦвет = цвЗеленый;
			Иначе			
				тзНеобходимыеПартии.сЦвет = цвЖелтый;							
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

//======================================================================
Процедура Клик_партии()
	текСтр = тзНеобходимыеПартии.ТекущаяСтрока();
	текКол = тзНеобходимыеПартии.ТекущаяКолонка();
	
	Если текКол="Пометка" Тогда		
		Пометка = тзНеобходимыеПартии.ПолучитьЗначение(ТекСтр,"Пометка");
		Если Пометка = 2 Тогда
			тзНеобходимыеПартии.Пометка=1;
		Иначе
			тзНеобходимыеПартии.Пометка=2;
		КонецЕсли;
	ИначеЕсли (текКол="ВСД_Продукция_Элемент") и (тзНеобходимыеПартии.ВидимостьКолонки("ДокРеализации") > 0) Тогда
		Если (ПустоеЗначение(тзНеобходимыеПартии.ВСД_Продукция_Элемент) = 1) Тогда
			//Выбрать и привязать
			ВыбСтрока = текСтр;
			ТекНоменклатура = тзНеобходимыеПартии.ПолучитьЗначение(ТекСтр,"Номенклатура");
			Продукция_элемент = СоздатьОбъект("Справочник.ВСД_Продукция_Элемент");
			Если Продукция_элемент.Выбрать("Укажите элемент для установки соответствия","ФормаСписка") = 0 Тогда
				Возврат;			    
			КонецЕсли;
			ТВопроса = "Установить соответствие "+ТекНоменклатура+" -> "+ Продукция_элемент.ТекущийЭлемент();
			Если Вопрос(ТВопроса,"Да+Нет") = "Да" Тогда
				СпрНом = СоздатьОбъект("Справочник.Номенклатура");
				СпрНом.НайтиЭлемент(ТекНоменклатура.ТекущийЭлемент());
				СпрНом.ВСД_Продукция_Элемент = Продукция_элемент.ТекущийЭлемент();
				СпрНом.Записать();
				тзНеобходимыеПартии.УстановитьЗначение(ВыбСтрока,"ВСД_Продукция_Элемент", Продукция_элемент.ТекущийЭлемент());
				Предупреждение("Необходимо перезаполнить партии !!!");
			КонецЕсли;
			//ОткрытьПодбор("Справочник.ВСД_Продукция_Элемент", "ФормаСписка");
		Иначе
			Эл = тзНеобходимыеПартии.ПолучитьЗначение(текСтр, текКол);	
			ОткрытьФорму(Эл);
		КонецЕсли;
	Иначе
		Эл = тзНеобходимыеПартии.ПолучитьЗначение(текСтр, текКол);	
		ОткрытьФорму(Эл);
	КонецЕсли;
	
	ОбновитьИнф();	
КонецПроцедуры

//======================================================================
Процедура ОтменитьВсеДокументы()
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		СписокДокументов.Пометка=0;		
	КонецЦикла;	 
	Попытка
		ТПДокументов.ОбновитьСтроки();
	Исключение
	КонецПопытки;
	ОбновитьИнф();
КонецПроцедуры

//======================================================================
Процедура ВыделитьВсеДокументы()
	спМеню = СоздатьОбъект("СписокЗначений");
	спМеню.ДобавитьЗначение("ВыделитьВсе", "Выделить все документы");
	спМеню.ДобавитьЗначение("ВыделитьСоздать", "Выделить пустые ВСД");
	спМеню.ДобавитьЗначение("ВыделитьНеОтправленные", "Выделить НЕ ОТПРАВЛЕННЫЕ");

	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "ВыделитьВсе" Тогда 
		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			СписокДокументов.Пометка=1;		
		КонецЦикла;	      
	ИначеЕсли зн = "ВыделитьСоздать" Тогда 
		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			СписокДокументов.Пометка=0;
			Если ( ПустоеЗначение(СписокДокументов.ВСД) = 1 ) Тогда 
				Если (ПустоеЗначение(СписокДокументов.Площадка)=0) Тогда 
					СписокДокументов.Пометка=1;		
				КонецЕсли;
			КонецЕсли;
			
			Если СписокДокументов.сЦвет = "" Тогда 
				СписокДокументов.Пометка=1;		
			КонецЕсли;
		КонецЦикла;	      
	ИначеЕсли зн = "ВыделитьНеОтправленные" Тогда 
		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			СписокДокументов.Пометка=0;
			Если ( ПустоеЗначение(СписокДокументов.ВСД) = 0 ) Тогда 
				Если ( СокрЛП(СписокДокументов.ВСД.Статус) <> "COMPLETED" ) Тогда 
					СписокДокументов.Пометка=1;		
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;	      
	КонецЕсли;
		
	тзНеобходимыеПартии.УдалитьСтроки();

	Попытка
		ТПДокументов.ОбновитьСтроки();
	Исключение
	КонецПопытки;	
	
	ОбновитьИнф();	
	
КонецПроцедуры

//========================= ТАБЛИЧНОЕ ПОЛЕ ДОКУМЕННТОВ =================

//======================================================================
Процедура ТПДокументовПриВыбореФлажка(ТабличноеПоле,Стр, Колонка, ТипРегиона)
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, СписокДокументов);
	СписокДокументов.Пометка = ?(СписокДокументов.Пометка = 1,0,1);
	ТабличноеПоле.ОбновитьСтроки();
	ОбновитьИнф();

	тзНеобходимыеПартии.УдалитьСтроки();
	СписокПартий.УдалитьСтроки();
КонецПроцедуры

//======================================================================
Процедура ТПДокументовПриАктивизацииСтроки(ТабличноеПоле)
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, СписокДокументов);
КонецПроцедуры

Процедура ТПДокументовВыбор()
	ГМ.ПриАктивизацииСтрокиТП(ТПДокументов, СписокДокументов);
	ГМ.ПриАктивизацииКолонкиТП(ТПДокументов, СписокДокументов);
	
	текСтр=СписокДокументов.ТекущаяСтрока();
	Если текСтр=0 Тогда Возврат; КонецЕсли;
	текКол = СписокДокументов.ТекущаяКолонка();
	
	Если текКол="Площадка" Тогда 
		ВыбСтрока = текСтр;
		Площадка = СписокДокументов.ПолучитьЗначение(ТекСтр,текКол);
		
		ХозСубъект = СписокДокументов.ПолучитьЗначение(текСтр, "ХозСубъект");
					
		тз = ГМ.ВыбратьВсеПлощадкиХС(ХозСубъект);
		
		СписокОтбора = СоздатьОбъект("СписокЗначений");
		
		Тз.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСТроку()=1 Цикл
			СписокОтбора.ДобавитьЗначение(тз.id);
		КонецЦикла;			
				
		ОткрытьПодбор("Справочник.ВСД_Площадка", "ФормаСписка", СписокОтбора,0, Площадка);
		
	ИначеЕсли текКол="ХозСубъект" Тогда 
		
		ХозСубъект = СписокДокументов.ПолучитьЗначение(текСтр, "ХозСубъект");	
		Если ПустоеЗначение(ХозСубъект)=1 Тогда 
			ОткрытьФорму("Справочник.ВСД_ХозСубъект");
		Иначе
			ОткрытьФорму(ХозСубъект);
		КонецЕсли;
		
	Иначе
		Эл = СписокДокументов.ПолучитьЗначение(текСтр, текКол);	
		ОткрытьФорму(Эл);
	КонецЕсли;
КонецПроцедуры

//======================================================================
Процедура ТПДокументовПриВыводеСтроки(ТабличноеПоле,ОформлениеСтроки,ДанныеСтроки,ТипРегиона)

	Если ТипРегиона = 3 Тогда
		ГМ.ВывестиФлажок(ОформлениеСтроки, ДанныеСтроки, "Пометка");
		
		Если (ПустоеЗначение(ДанныеСтроки.ВСД)=0) Тогда
			Если СокрЛП(ДанныеСтроки.ВСД.Статус)="COMPLETED" Тогда
				ОформлениеСтроки.ЦветФона = 65280;	// Зеленый
			КонецЕсли;
		ИначеЕсли ПустоеЗначение(ДанныеСтроки.ХозСубъект.GUID)=1 Тогда 			
			ОформлениеСтроки.ЦветФона = 255;	// Красный
//			ОформлениеСтроки.ЦветФона = 8421631;	// Красный
		ИначеЕсли (ПустоеЗначение(ДанныеСтроки.Площадка)=0) Тогда
			Если (ПустоеЗначение(ДанныеСтроки.Площадка.GUID)=0)  Тогда 
				//ДанныеСтроки.сЦвет = "";							
			КонецЕсли;
		Иначе			
			ОформлениеСтроки.ЦветФона = 8454143;	// Желтый
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//=========================== ПЕЧАТНЫЕ ФОРМЫ =============================

Процедура Реестр()
	Таб = СоздатьОбъект("Таблица");
	Таб.ВывестиСекцию("Шапка|Осн");
	Если ДобавлятьУпаковки = 1 Тогда
		Таб.ПрисоединитьСекцию("Шапка|ВесМест");
	КонецЕсли;
	Таб.ПрисоединитьСекцию("Шапка|Хвост");
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		
		Таб.ВывестиСекцию("Строка|Осн");
		Если ДобавлятьУпаковки = 1 Тогда
			Таб.ПрисоединитьСекцию("Строка|ВесМест");
		КонецЕсли;
		Таб.ПрисоединитьСекцию("Строка|Хвост");
	КонецЦикла;

	Таб.Опции(0,0,0,0);
	Таб.ТолькоПросмотр();
	Таб.Показать("Реестр");	
КонецПроцедуры
              
Процедура ПечатьСокрВСД()
	
	тз = СоздатьОбъект("ТаблицаЗначений");
	СписокДокументов.Выгрузить(тз);
	тз.ВыбратьСтроки();
	Пока тз.ПолучитьСтроку() = 1 Цикл
		Если (тз.Пометка<>1) или (Пустоезначение(тз.ВСД)=1) Тогда
			Продолжить;	
		КонецЕсли;
		ГМ.ПечатьСокрВСД(тз.ВСД.ТекущийДокумент());
	КонецЦикла;
КонецПроцедуры

Процедура МенюПечать()
	спМеню = СоздатьОбъект("СписокЗначений");
	спМеню.ДобавитьЗначение("Реестр", "Реестр");
	спМеню.ДобавитьЗначение("СокрВСД", "Сокращенный ВСД");
	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "Реестр" Тогда 
		Реестр();
	ИначеЕсли зн = "СокрВСД" Тогда 
		ПечатьСокрВСД();
	КонецЕсли;
		
КонецПроцедуры

//=========================== ФУНКЦИИ КНОПОК =============================

Процедура МенюПлощадки()
		
	//меню
	СписокДействийВСД = СоздатьОбъект("СписокЗначений");
	СписокДействийВСД.ДобавитьЗначение("ЗагрузитьПоХС","Загрузить площадки по ХС");
	СписокДействийВСД.ДобавитьЗначение("НайтиПоИмени","Найти площадки по названию");
	СписокДействийВСД.ДобавитьЗначение("СоздатьХС","Создать ХозСубъект");
	СписокДействийВСД.ДобавитьЗначение("СоздатьПлощадку","Создать Площадку");
		
	стр=0; Зн="";
	Если СписокДействийВСД.ВыбратьЗначение(Зн, "", стр, 60, 1) = 1 Тогда	
		
		Если Зн ="ЗагрузитьПоХС"  Тогда 
			ГМ.ЗагрузитьПлощадки(СписокДокументов.ХозСубъект);		
			ТПДокументов.ОбновитьСтроки();
		ИначеЕсли Зн = "НайтиПоИмени" Тогда 
			Попытка				
				ГМ2.НайтиПлощадкиПоНазванию( СписокДокументов.ХозСубъект, Регион, 1);		
			Исключение
				//если нет ГМ2 - вызываем ГМ1
				ГМ.НайтиПлощадкиПоНазванию( СписокДокументов.ХозСубъект, Регион, 1);		
			КонецПопытки;
			ТПДокументов.ОбновитьСтроки();
		ИначеЕсли Зн = "СоздатьХС" Тогда 
			ГМ.СоздатьХС( СписокДокументов );		
			ТПДокументов.ОбновитьСтроки();
		ИначеЕсли Зн = "СоздатьПлощадку" Тогда 
			ГМ.СоздатьПлощадку( СписокДокументов );		
			ТПДокументов.ОбновитьСтроки();
		КонецЕсли;
	КонецЕсли;
	
	//ПриВыбореЗакладки(Форма.Закладки.ТекущаяСтрока(),"Реализации");
КонецПроцедуры

//======================================================================
Процедура ЗаполнитьСписокДокументов()
		
	НазваниеВидаСправочникаФирмы	= гм.ПолучитьКонстанту("НазваниеВидаСправочникаФирмы");
	ВидДокументаПеремещениеТМЦ 		= гм.ПолучитьКонстанту("НазваниеВидаДокументаПеремещениеТМЦ"); 
	ПропускатьПустыеСвойства 		= гм.ПолучитьКонстанту("ПропускатьПустыеСвойства"); 
	НазваниеРеквизитаНоменклатура	= гм.ПолучитьКонстанту("НазваниеРеквизитаНоменклатура");
	НазваниеРеквизитаКоличество 	= гм.ПолучитьКонстанту("НазваниеРеквизитаКоличество");

	ФирмаИмяРеквизита = "";
	ФирмаОбщийРеквизит = ГМ.ПолучитьИмяРеквизитаФирма(ВидДокументаПеремещениеТМЦ, ФирмаИмяРеквизита);

	Если ПустаяСтрока(НазваниеВидаСправочникаФирмы) = 1 Тогда
		Сообщить("В Параметрах не указано НазваниеВидаСправочникаФирмы!","!"); //Возврат "";
	КонецЕсли;	
	Если ВидДокументаПеремещениеТМЦ = "" Тогда
		Предупреждение("Не заполнена константа ВидДокументаПеремещениеТМЦ"); Возврат;
	КонецЕсли;

	ВидСправочникаСклады = Метаданные.Справочник("ВСД_Площадка").Реквизит("Склад").Вид;
	Если ПустаяСтрока(ВидСправочникаСклады) = 1 Тогда
		ВидСтравочникаСклады = Метаданные.Справочник("ВСД_СкладыПлощадок").Реквизит("Склад").Вид;
	КонецЕсли;      
	Если ПустаяСтрока(ВидСправочникаСклады) = 1 Тогда
		Предупреждение("Не известен ВидСправочникаСклады"); Возврат;
	КонецЕсли;      

	ФильтрПоСкладу = 0;
	Если флФильтрПоСкладу = 1 Тогда
		Если ПустаяСтрока(ВидСтравочникаСклады) = 0 Тогда
			СписокСкладов = СоздатьОбъект("СписокЗначений");
			Если ПустоеЗначение(Отправитель_Площадка.Склад) = 0 Тогда
				СписокСкладов.ДобавитьЗначение(Отправитель_Площадка.Склад);
			КонецЕсли;
			Спр = СоздатьОбъект("Справочник.ВСД_СкладыПлощадок");
			Спр.ИспользоватьВладельца(Отправитель_Площадка);         
			Спр.ВыбратьЭлементы();
			Пока Спр.ПолучитьЭлемент() = 1 Цикл
				Если ПустоеЗначение(Спр.Склад) = 0 Тогда
					СписокСкладов.ДобавитьЗначение(Спр.Склад);
				КонецЕсли;
			КонецЦикла;
			
			Если СписокСкладов.РазмерСписка() > 0 Тогда
				ФильтрПоСкладу = 1;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ГМ.ЭтоSQL=1 Тогда 	
			
		ТекстЗапроса = "
		|SELECT
		|    Жур.IDDoc as [Док $Документ],
		|    Жур.IDDocDef as Док_вид,
		|	ДокВСД.IDDoc as [ДокВСД $Документ.ВСД_транзакция],
		|	Жур.DocNo as [НомерДок],
		|	$ДокР.Склад as [Склад $Справочник.Склады], 
		|	$ДокР.СкладПолучатель as [СкладПолучатель $Справочник.Склады],
		|	SUM($ДокСтроки."+ НазваниеРеквизитаКоличество +") [Количество]
		|FROM
		|    _1SJourn Жур
		|INNER JOIN 
		|	$ДокументСтроки."+ВидДокументаПеремещениеТМЦ+" as ДокСтроки 
		|		ON Жур.IDDoc = ДокСтроки.IDDoc
		|		AND $ДокСтроки."+НазваниеРеквизитаКоличество+" > 0
		|LEFT JOIN 
		|	$Документ."+ВидДокументаПеремещениеТМЦ+" as ДокР 
		|		ON Жур.IDDoc = ДокР.IDDoc
		|Left join 
		|	$Документ.ВСД_транзакция as ДокВСД
		|		ON SUBSTRING($ДокВСД.ДокОснование, 5, 9) = Жур.IDDoc
		|";
		Если ПропускатьПустыеСвойства =1 Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|	inner join $Справочник.Номенклатура as Ном 
			|		on $ДокСтроки.Номенклатура= Ном.ID
			|		and $Ном.ВСД_Продукция_Элемент <> $ПустойИД
			|";
		КонецЕсли;
		ТекстЗапроса=ТекстЗапроса+"
		|WHERE
		|    Жур.Date_Time_IDDoc BETWEEN :НачДата AND :КонДата~ 
		|    AND Жур.IDDocDef = $ВидДокумента."+ВидДокументаПеремещениеТМЦ+"
		//|	and (
		//|		$ДокВСД.Статус <> 'Аннулирова' OR
		//|		$ДокВСД.Статус Is Null 
		//|		)
		|	%УсловиеПоФирме%
		|	%УсловиеПоСкладу%
		|
		|GROUP BY
		|	Жур.IDDoc,
		|	Жур.IDDocDef,
		|	ДокВСД.IDDoc,
		|	Жур.DocNo,
		|	$ДокР.Склад,
		|	$ДокР.СкладПолучатель
		|order by Жур.DocNo
		|";
		
		//условие по фирме
		УсловиеПоФирме="";
		Если ПустоеЗначение(ВыбФирма) = 0 Тогда		
			Если ФирмаОбщийРеквизит > 0 Тогда
				Если ФирмаОбщийРеквизит = 1 Тогда
					УсловиеПоФирме = " and Жур.$ОбщийРеквизит."+ФирмаИмяРеквизита+" = :ВыбФирма";
				Иначе
					УсловиеПоФирме = " and $ДокР."+ФирмаИмяРеквизита+" = :ВыбФирма";
				КонецЕсли;
				ГМ.RS.УстановитьТекстовыйПараметр("ВыбФирма", ВыбФирма);
			КонецЕсли;
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоФирме%", УсловиеПоФирме);
		
		//условие по складу
		УсловиеПоСкладу = "";
		Если ФильтрПоСкладу = 1 Тогда
			Если СписокСкладов.РазмерСписка() = 1 Тогда
				УсловиеПоСкладу = " and $ДокР.Склад = :ВыбСклад";
				ГМ.RS.УстановитьТекстовыйПараметр("ВыбСклад", СписокСкладов.ПолучитьЗначение(1));
			Иначе
				УсловиеПоСкладу = " and $ДокР.Склад IN (SELECT Val FROM #Склады) ";
				ГМ.RS.УложитьСписокОбъектов(СписокСкладов, "#Склады",ВидСтравочникаСклады);
			КонецЕсли;
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоСкладу%", УсловиеПоСкладу);
		
		ГМ.RS.УстановитьТекстовыйПараметр("НачДата", НачДата);
		ГМ.RS.УстановитьТекстовыйПараметр("КонДата", КонДата);
		     
		
		ГМ.RS.Отладка(0);
		ТЗ = ГМ.RS.ВыполнитьИнструкцию(ТекстЗапроса);
		//ТЗ.ВыбратьСтроку();
	  	
	Иначе	
			
		Запрос = ГМ.базаДанных.НовыйЗапрос();
	
		ТекстЗапроса = "
		|SELECT
		|    Жур.IDDoc as [Док :Документ],
		|    Жур.IDDocDef as Док_вид,
		|	Жур.DocNo as [НомерДок],
		|	ДокВСД.IDDoc as [ДокВСД $Документ.ВСД_транзакция],
		|	ДокР.Склад as [Склад :Справочник."+ВидСправочникаСклады+"],
		|	ДокР.СкладПолучатель as [СкладПолучатель :Справочник."+ВидСправочникаСклады+"],		
		|	SUM(ДокСтроки."+ НазваниеРеквизитаКоличество +") as [Количество]
		|FROM
		|    Журнал Жур
		|INNER JOIN 
		|	[ДокументСтроки."+ВидДокументаПеремещениеТМЦ+"] as ДокСтроки 
		|		ON Жур.IDDoc = ДокСтроки.IDDoc
		|		AND ДокСтроки."+НазваниеРеквизитаКоличество+" > 0";
		Если ПропускатьПустыеСвойства = 1 Тогда
			ТекстЗапроса=ТекстЗапроса+"
			|inner join [Справочник.Номенклатура] as Ном 
			|	on ДокСтроки."+НазваниеРеквизитаНоменклатура+" = Ном.ID
			|	and Ном.ВСД_Продукция_Элемент <> '     0   '
			|";
		КонецЕсли;
		ТекстЗапроса=ТекстЗапроса+"
		|INNER JOIN 
		|	[Документ."+ВидДокументаПеремещениеТМЦ+"] as ДокР 
		|		ON Жур.IDDoc = ДокР.IDDoc
		|Left join 
		|	[Документ.ВСД_транзакция] as ДокВСД
		|		ON SUBSTR(ДокВСД.ДокОснование, 5, 9) = Жур.IDDoc
		|WHERE
		|    Жур.idx_date_time_iddoc BETWEEN :НачДата AND :КонДата~ 
		|   AND Жур.IDDocDef = :ВидДокумента."+ВидДокументаПеремещениеТМЦ+"
		//| 	AND COALESCE(ДокВСД.Статус,'*') <> 'Аннулирова'
		//|	%УсловиеПоФирме%
		//|	%УсловиеПоСкладу%
		|
		|GROUP BY
		|	Жур.IDDoc,
		|	Жур.IDDocDef,
		|	Жур.DocNo,
		|	ДокР.Склад,
		|	ДокР.СкладПолучатель
		|order by Жур.DocNo
		|";
		
		//условие по фирме
		УсловиеПоФирме="";
		Если ПустоеЗначение(ВыбФирма) = 0 Тогда		
			Если ФирмаОбщийРеквизит > 0 Тогда
				Если ФирмаОбщийРеквизит = 1 Тогда
					УсловиеПоФирме = " and Жур."+ФирмаИмяРеквизита+" = :ВыбФирма";
				ИначеЕсли ФирмаОбщийРеквизит = 2 Тогда
					УсловиеПоФирме = " and ДокР."+ФирмаИмяРеквизита+" = :ВыбФирма";
				КонецЕсли;
				Запрос.Подставлять("ВыбФирма", ВыбФирма);
			КонецЕсли;
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоФирме%", УсловиеПоФирме);

		//условие по складу
		УсловиеПоСкладу = "";
		Если ФильтрПоСкладу = 1 Тогда
			Если СписокСкладов.РазмерСписка() = 1 Тогда
				УсловиеПоСкладу = " and ДокР.Склад = :ВыбСклад";
				Запрос.Подставлять("ВыбСклад", СписокСкладов.ПолучитьЗначение(1));
			Иначе
				УсловиеПоСкладу = " and ДокР.Склад IN (SELECT Val FROM Склады) ";
				ГМ.базаДанных.УложитьОбъекты(СписокСкладов, "Склады",ВидСтравочникаСклады);
			КонецЕсли;
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеПоСкладу%", УсловиеПоСкладу);

		Запрос.Подставлять("НачДата", НачДата);
		Запрос.Подставлять("КонДата", КонДата);
	
		ТЗ = Запрос.ВыполнитьЗапрос(ТекстЗапроса);
		//ТЗ.ВыбратьСтроку();
	КонецЕсли;
		
	СписокДокументов.УдалитьСтроки();
	
	тзДок = СоздатьОбъект("ТаблицаЗначений");
	СписокДокументов.Выгрузить(тзДок);
	
	тз.ВыбратьСтроки();
	Ном=0;
	ПредДок="";
	Пока тз.ПолучитьСтроку()=1 Цикл 
		тзДок.НоваяСтрока();
		тзДок.Пометка=1;
		тзДок.Контрагент = тз.Склад;
		тзДок.Грузополучатель = тз.СкладПолучатель;
		тзДок.Док = тз.Док;
		тзДок.Колво = тз.Количество;
		тзДок.Вес = тз.Количество;
		Попытка			
			тзДок.КолвоМест = тз.КоличествоМест;
		Исключение			
		КонецПопытки;
				
		тзДок.ХозСубъект = Отправитель_ХозСубъект;
		тзДок.Площадка = ГМ.НайтиПлощадкуПоСкладу(тзДок.Грузополучатель);

		Попытка
			Если тз.ДокВСД.ПометкаУдаления()=0 Тогда 
				тзДок.ВСД = тз.ДокВСД;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
	КонецЦикла; 
	
	тзДок.Выгрузить(СписокДокументов);
	
	Попытка
		ТПДокументов.ОбновитьСтроки();
	Исключение
		//Сообщить(ОписаниеОшибки());
	КонецПопытки;
	      
	//РаскраситьСписокДокументов( тзДок, ТекСтрока );
	ОбновитьИнф();
КонецПроцедуры

процедура ВыбратьПериод()
	если ВвестиПериод(НачДата,КонДата)=1 тогда
		ЗаголовокНадпись();
		СписокДокументов.УдалитьСтроки();
		ЗаполнитьСписокДокументов();
	конецесли;
конецпроцедуры

Процедура СоздатьВСД()
	
	Если СписокПартий.КоличествоСтрок()=0 Тогда
		Сообщить("Не заполнена таблица партий.","!");
		Возврат;
	КонецЕсли;
	
	ПарамКолонкаСортировкиПартииСписания	= гм.ПолучитьКонстанту("ПарамКолонкаСортировкиПартииСписания");
	ПарамЗнакСортировкиУбывание	= гм.ПолучитьКонстанту("ПарамЗнакСортировкиУбывание");
	ПарамЗаполнятьВСДБезПартий	= гм.ПолучитьКонстанту("ПарамЗаполнятьВСДБезПартий");
	
	СзПродукцияЭлементы = СоздатьОбъект("СписокЗначений");
	СписокПартий.Выгрузить(СзПродукцияЭлементы,,,"ВСД_Продукция_Элемент");
	// Получим нужные нам партии по фильтру
	ТзРаспределяемыеПартии = ГМ.ПолучитьАкуальныеПартииИзСправочника(СзПродукцияЭлементы,Отправитель_Площадка,Отправитель_ХозСубъект,СокрЛП(ПарамКолонкаСортировкиПартииСписания),ПарамЗнакСортировкиУбывание);

	Если ПустоеЗначение(ТзРаспределяемыеПартии) = 1 Тогда
		Сообщить("Нет актуальных партий для создания документов");
		Возврат;
	КонецЕсли;
    //ТзРаспределяемыеПартии.ВыбратьСтроку();//ТЕСТ
	// теперь нужно уменьшить полученные данные на кол-во созданных, но не отправленных ВСД
	УменьшитьНаРаспределенныеПартии(ТзРаспределяемыеПартии);
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка<>1 Тогда
			Продолжить;
		КонецЕсли;

		Если (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда       
			Если (ПустоеЗначение(СписокДокументов.ВСД.Статус) = 1) Тогда 
				Сообщить("Для "+СписокДокументов.Док+" ВСД уже создан, но не отправлен","i");
				Продолжить;
			КонецЕсли;		
		КонецЕсли;		

		Состояние("Заполнение списка партий ВСД ");
		ХС = СписокДокументов.ХозСубъект;
		Площадка = СписокДокументов.Площадка;
		
		// создание кастомного ВСД
		Если ФС.СуществуетФайл(ГМ.КаталогМодуля+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_транзакция.ert")=1 Тогда
			
			//{ переопределение функции создания ВСД 
			// интеграция переопределяется в Меркурий_Подключаемый_Создание_ВСД_транзакция.ert
			//
			СписокПараметров = СоздатьОбъект("СписокЗначений");
			СписокПараметров.ДобавитьЗначение(СписокДокументов.Док,				"ДокОснование");    
			СписокПараметров.ДобавитьЗначение(СписокДокументов.Контрагент,		"Контрагент");    
			СписокПараметров.ДобавитьЗначение(СписокДокументов.Грузополучатель,	"Грузополучатель");    
			СписокПараметров.ДобавитьЗначение(Отправитель_ХозСубъект,			"Отправитель_ХозСубъект");    
			СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,				"Отправитель_Площадка");    
			СписокПараметров.ДобавитьЗначение(Перевозчик_ХозСубъект,			"Перевозчик_ХозСубъект");    
			СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,				"Отправитель_Площадка");    
			СписокПараметров.ДобавитьЗначение( ХС,				"ХС");    
			СписокПараметров.ДобавитьЗначение( Площадка,		"Площадка");    
			СписокПараметров.ДобавитьЗначение( СписокДокументов.Колво,			"Колво");    
			Попытка СписокПараметров.ДобавитьЗначение( СписокДокументов.КолвоМест,		"КолвоМест"); Исключение КонецПопытки;    
			
			СписокПараметров.ДобавитьЗначение( ВСД_Экспертиза,					"ВСД_Экспертиза");    
			СписокПараметров.ДобавитьЗначение( ВСД_Местность,					"ВСД_Местность");
			СписокПараметров.ДобавитьЗначение( ВСД_ОсобыеОтметки,				"ВСД_ОсобыеОтметки");    
			
			СписокПараметров.ДобавитьЗначение( тзНеобходимыеПартии,					"тзНеобходимыеПартии");    
				
			ОткрытьФормуМодально("Отчет",СписокПараметров,ГМ.КаталогМодуля+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_транзакция.ert");
			
			ДокументСсылка = "";
			
			Если ТипЗначенияСтр(СписокПараметров)  = "СписокЗначений" Тогда				
				ДокументСсылка = СписокПараметров.Получить("ДокументСсылка");				
			КонецЕсли;  
			
			Если ПустоеЗначение(ДокументСсылка)=1 Тогда
				Предупреждение("В подключаемом модуле не удалось создать ВСД","!");
				//Возврат "";
			КонецЕсли;
			//}
			
		Иначе
			// стандартное создание ВСД
			ДокОснование = СписокДокументов.Док; 	
				
			ДокВСД = СоздатьОбъект("Документ.ВСД_транзакция");
			ДокВСД.Новый();
			ДокВСД.ДокОснование = СписокДокументов.Док;			
			ДокВСД.ДатаДок = ДокВСД.ДокОснование.ДатаДок;			
			ДокВСД.Отправитель_ХозСубъект = Отправитель_ХозСубъект;
			ДокВСД.Отправитель_Площадка = Отправитель_Площадка;			
			ДокВСД.Получатель_ХозСубъект = ХС;		
			ДокВСД.Получатель_Площадка = Площадка;					
			ДокВСД.Перевозчик_ХозСубъект = Перевозчик_ХозСубъект;
						
			РеквСвязДок = ГМ.ПолучитьСвязанныйДокумент(ДокОснование,"ТТН");			
			ДокВСД.ТтнСерия = РеквСвязДок.Получить("ДокСерия");
			ДокВСД.ТтнНомер = РеквСвязДок.Получить("ДокНомер");
			ДокВСД.ТтнДата = РеквСвязДок.Получить("ДокДата");
			
			ДокВСД.номерАвто = ГМ.ПолучитьНомерАвто(ДокВСД.ДокОснование);
			
			Автор = ГМ.ПолучитьАвтора();
			Попытка	ДокВСД.Автор = Автор;	Исключение 	КонецПопытки;
			Попытка	ДокВСД.Филиал = Автор.Филиал;	Исключение	КонецПопытки;
				
			ДокВСД.Экспертиза 		= ВСД_Экспертиза;
			ДокВСД.Местность 		= ВСД_Местность;
			ДокВСД.ОсобыеОтметки 	= ВСД_ОсобыеОтметки;
			ДокВСД.cargoExpertized  = 1;
			ДокВСД.cargoInspected  	= 1;			

			тз = ГМ.ЗаполнитьПартииПоТЧДокумента2(ДокОснование,Отправитель_Площадка, Отправитель_ХозСубъект, ТзРаспределяемыеПартии,ПарамЗаполнятьВСДБезПартий);
			ДокВСД.ЗагрузитьТабличнуюЧасть(тз);
			//Терм состояние проставим
			тз.ВыбратьСтроки();
			Пока тз.ПолучитьСтроку() = 1 Цикл
				ДокВСД.ТермическоеСостояние = Мин( тз.Продукция_Элемент.ТермическиеУсловияПеревозки, ДокВСД.ТермическоеСостояние);    
			КонецЦикла;
			ДокВСД.ТермическоеСостояние = ?( ДокВСД.ТермическоеСостояние = 0, 1, ДокВСД.ТермическоеСостояние);
			Если ДокВСД.КоличествоСтрок() = 0 Тогда
			    Возврат;
			КонецЕсли;
			ДокВСД.Записать();
			ДокументСсылка = ДокВСД.ТекущийДокумент();
			
		КонецЕсли;
		
		СписокДокументов.ВСД = ДокументСсылка;
		
		Сообщить("["+СписокДокументов.Грузополучатель+"] создан документ "+СписокДокументов.ВСД,"i");		
	КонецЦикла;

	Попытка
		ТПДокументов.ОбновитьСтроки();
	Исключение
	КонецПопытки;	
	СписокПартий.УдалитьСтроки();
	тзНеобходимыеПартии.УдалитьСтроки();
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

Процедура СоздатьВСД_ПоТзРаспределения()
	Если тзНеобходимыеПартии.ВидимостьКолонки("ДокРеализации") < 0 Тогда
		Сообщить("Включите режим Распределить партии","i");
		Возврат;
	КонецЕсли;
	Если тзНеобходимыеПартии.КоличествоСтрок()=0 Тогда
		Сообщить("Не заполнена таблица с распределенными партиями.","!");
		Возврат;
	КонецЕсли;
	
	Если ФС.СуществуетФайл(ГМ.КаталогМодуля+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_транзакция.ert")=1 Тогда
		Сообщить("Данная ф-ция временно недоступна при наличии подключаемого модуля Создания ВСД");
		Возврат;
	КонецЕсли;
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка<>1 Тогда
			Продолжить;
		КонецЕсли;

		Если (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда       
			Если (ПустоеЗначение(СписокДокументов.ВСД.Статус) = 1) Тогда 
				Сообщить("Для "+СписокДокументов.Док+" ВСД уже создан, но не отправлен","i");
				Продолжить;
			КонецЕсли;		
		КонецЕсли;		
        
		стр = 0;
		Если тзНеобходимыеПартии.НайтиЗначение(СписокДокументов.Док,стр,"ДокРеализации") = 0 тогда
			Сообщить("Для "+СписокДокументов.Док+" нет данных в тз Распределенных партий","i");
			Продолжить;
		КонецЕсли;
		
		тзНеобходимыеПартии.ПолучитьСтрокуПоНомеру(стр);
		
		Состояние("Создание ВСД ");
		ХС = СписокДокументов.ХозСубъект;
		Площадка = СписокДокументов.Площадка;
		
		ДокОснование = СписокДокументов.Док; 	
		ДокВСД = СоздатьОбъект("Документ.ВСД_транзакция");
		ДокВСД.Новый();
		ДокВСД.ДокОснование = ДокОснование;

		ВыбФирма = ГМ.ПолучитьФирмуИзКонтекста(СписокДокументов.Док);
	
		ФирмаИмяРеквизита = "";
		ГМ.ПолучитьИмяРеквизитаФирма(ДокВСД.Вид(), ФирмаИмяРеквизита);
		Если ФирмаИмяРеквизита <> "" Тогда
			ДокВСД.УстановитьАтрибут(ФирмаИмяРеквизита, ВыбФирма);
		КонецЕсли;
		
		Если глЕстьРеквизитШапки("Автор", ДокВСД.Вид()) = 1 Тогда
			ДокВСД.Автор = ГМ.ПолучитьАвтора();
		КонецЕсли;
		
		ДокВСД.ДатаДок = ДокОснование.ДатаДок;			
		ДокВСД.Отправитель_ХозСубъект = Отправитель_ХозСубъект;
		ДокВСД.Отправитель_Площадка = Отправитель_Площадка;			
		ДокВСД.Получатель_ХозСубъект = ХС;		
		ДокВСД.Получатель_Площадка = Площадка;					
		ДокВСД.Перевозчик_ХозСубъект = Перевозчик_ХозСубъект;
			
		РеквСвязДок = ГМ.ПолучитьСвязанныйДокумент(ДокОснование,"ТТН");			
		ДокВСД.ТтнСерия = РеквСвязДок.Получить("ДокСерия");
		ДокВСД.ТтнНомер = РеквСвязДок.Получить("ДокНомер");
		ДокВСД.ТтнДата = РеквСвязДок.Получить("ДокДата");
		
		ДокВСД.номерАвто = ГМ.ПолучитьНомерАвто(ДокВСД.ДокОснование);

		ДокВСД.Местность 		= ВСД_Местность;
		ДокВСД.ОсобыеОтметки 	= ВСД_ОсобыеОтметки;
		ДокВСД.cargoExpertized  = 1;
		ДокВСД.cargoInspected  	= 1;			
		ДокВСД.ТермическоеСостояние = 4;
		
		РеквСвязДок = ГМ.ПолучитьСвязанныйДокумент(ДокОснование,"Заказ");			
		ДокВСД.НомерЗаказаПокупателя = РеквСвязДок.Получить("ДокНомер");
		ДокВСД.ДатаЗаказаПокупателя = РеквСвязДок.Получить("ДокДата");
			
		Пока (стр <= тзНеобходимыеПартии.КоличествоСтрок()) и (СписокДокументов.Док = тзНеобходимыеПартии.ДокРеализации) Цикл
			ДокВСД.НоваяСтрока();				
			ДокВСД.Номенклатура = тзНеобходимыеПартии.Номенклатура;
			ДокВСД.Партия = тзНеобходимыеПартии.Партия;
			ДокВСД.Количество = тзНеобходимыеПартии.КолвоСписания;
			ДокВСД.ЕдиницаИзмерения = ДокВСД.Партия.ЕдиницаИзмерения;
				
			ДокВСД.Продукция = ДокВСД.Партия.Продукция;
			ДокВСД.ВидПродукции = ДокВСД.Партия.ВидПродукции;
			ДокВСД.Продукция_Элемент 	= ДокВСД.Номенклатура.ВСД_Продукция_Элемент;
			ДокВСД.ТермическоеСостояние = Мин( ДокВСД.Продукция_Элемент.ТермическиеУсловияПеревозки, ДокВСД.ТермическоеСостояние);
			ДокВСД.НаименованиеПродукции = ДокВСД.Продукция_Элемент.Наименование;
			ГМ.ЗаполнитьСтрокуДокумента( ДокВСД, тзНеобходимыеПартии, ДокВСД);
			стр = стр + 1;
			Если стр > тзНеобходимыеПартии.КоличествоСтрок() Тогда
			    Прервать;
			КонецЕсли;
			тзНеобходимыеПартии.ПолучитьСтрокуПоНомеру(стр);		    
		КонецЦикла;
		ДокВСД.ТермическоеСостояние = ?( ДокВСД.ТермическоеСостояние = 0, 1, ДокВСД.ТермическоеСостояние);
		
		Если ДокВСД.КоличествоСтрок() = 0 Тогда
		    Продолжить;
		КонецЕсли;
			
		ДокВСД.Записать();
		ДокументСсылка = ДокВСД.ТекущийДокумент();
		
		СписокДокументов.ВСД = ДокументСсылка;
		
		Сообщить("["+СписокДокументов.Грузополучатель+"] создан документ "+СписокДокументов.ВСД,"i");		
	КонецЦикла;
	СписокПартий.УдалитьСтроки();
	тзНеобходимыеПартии.УдалитьСтроки();
	ЗаполнитьСписокДокументов();
КонецПроцедуры

//======================================================================
Процедура ПриИзмененииФирмы()
	ГМ.Инициализация(Контекст);        
	ГМ.ЗагрузитьПараметрыВФорму(Контекст);

	СписокДокументов.УдалитьСтроки();
	СписокПартий.УдалитьСтроки();
	ЗаполнитьСписокДокументов();	
КонецПроцедуры

//======================================================================
Процедура ПриИзмененииСФ()
	Если СписокФирм.ТекущаяСтрока() <> 0 Тогда
		ВыбФирма = СписокФирм.ПолучитьЗначение(СписокФирм.ТекущаяСтрока());
		ПриИзмененииФирмы();
	КонецЕсли;
КонецПроцедуры // ПриИзмененииСФ

Процедура ЗаполнитьПартии()
	ВремТЗПодобранныеПартии = "";
	Если ПустоеЗначение(Отправитель_ХозСубъект)=1 Тогда
		Сообщить("Не выбран параметр Отправитель_ХозСубъект ","!");			
		Возврат;
	КонецЕсли;
	Если ПустоеЗначение(Отправитель_Площадка)=1 Тогда
		Сообщить("Не выбран параметр Отправитель_Площадка ","!");			
		Возврат;
	КонецЕсли;
	
	_Подробно = 0;	

	Форма.Закладки.ТекущаяСтрока(2);
	Форма.ИспользоватьСлой("Основной, Партии");
		
	СписокПартий.УдалитьСтроки();
	
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка<>1 Тогда
			Продолжить;
		КонецЕсли;

		Если (ПустоеЗначение(СписокДокументов.ВСД)=0) Тогда       
			Сообщить("Уже создан "+СписокДокументов.ВСД+" Пропускаю");
		    Продолжить;
		КонецЕсли;		

		Состояние("Заполнение списка партий ВСД ");
		
		тз = ГМ.ВыгрузитьТч(СписокДокументов.Док);
				
		тз.ВыбратьСтроки();
		Пока тз.ПолучитьСтроку() = 1 Цикл			
			СписокПартий.НоваяСтрока();
			СписокПартий.ДокРеализации = тз.ДокРеализации;
			СписокПартий.Номенклатура = тз.Номенклатура;
			СписокПартий.ВСД_Продукция_Элемент = тз.ВСД_Продукция_Элемент;
			СписокПартий.КолвоСписания = тз.Количество;
			Попытка					
				//СписокПартий.КолвоМестСписания = тз.КоличествоМест;
			Исключение
			КонецПопытки;

		КонецЦикла;
		
	КонецЦикла;
	
	ВывестиТЗПартийВариант2();

КонецПроцедуры

Функция ПроверитьВСД(ВСД)
	Рез =1;
	Если ПустоеЗначение(ВСД)=1 Тогда
		Возврат 0;
	КонецЕсли;
	Если (СокрЛП(ВСД.Статус)="COMPLETED") Тогда
		Возврат 0;
	КонецЕсли;
	Если ПустоеЗначение(ВСД.ТермическоеСостояние)=1 Тогда
		Сообщить("ВСД "+ВСД+" не указано ТермическоеСостояние","!");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
КонецФункции


Процедура ОтправитьВСД()
	
	СписокВСД = СоздатьОбъект("СписокЗначений");
	СписокДокументов.ВыбратьСтроки();
	Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
		Если СписокДокументов.Пометка = 1 Тогда 		
			Если ПроверитьВСД(СписокДокументов.ВСД)=1 Тогда 
				СписокВСД.ДобавитьЗначение(СписокДокументов.ВСД);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации("Меркурий отправка исходящих ВСД ");

	ГМ.ОтправитьВсе_ВСД_Транзакция(СписокВСД, НачДата, КонДата);

	ТПДокументов.ОбновитьСтроки();
	ОбновитьИнф();	
	
КонецПроцедуры

Процедура АннулироватьВСД()
	Если Вопрос("Вы уверены, что хотите аннулировать выбранные ВСД?",4,30)=6 Тогда 
		Сообщить("Выполняется аннулирование ВСД");
		
		СписокДокументов.ВыбратьСтроки();
		Пока СписокДокументов.ПолучитьСтроку() = 1 Цикл
			Если СписокДокументов.Пометка = 1 Тогда 
				ГМ.Аннулировать_ВСД_транзакция(СписокДокументов.ВСД);
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьСписокДокументов();
	КонецЕсли;
КонецПроцедуры

Процедура ПолучитьПартии()
	ЗаписьЖурналаРегистрации("Меркурий получение партий ");
	
	ГМ.ПолучитьПартии(Отправитель_Площадка);
	
КонецПроцедуры

Процедура Создать_ВСД_Производство()
	Если тзНеобходимыеПартии.ВидимостьКолонки("ВСД_Производство") < 0 Тогда
		Сообщить("Отключите режим распределения партий","i");
		Возврат;
	КонецЕсли;
	
	ВремСписокПартий = СоздатьОбъект("ТаблицаЗначений");
	тзНеобходимыеПартии.Выгрузить(ВремСписокПартий);
	ВремСписокПартий.ВыбратьСтроки();

	Пока ВремСписокПартий.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(ВремСписокПартий.ВСД_Производство)=0 Тогда
			Сообщить("В строке партий № "+ВремСписокПартий.НомерСтроки+" указан документ. Пропускаем...");
			Продолжить;
		КонецЕсли;
		Если ВремСписокПартий.Колво >= ВремСписокПартий.КолвоСписания Тогда
		    Продолжить;
		КонецЕсли;
		Если ПустоеЗначение(ВремСписокПартий.ВСД_Продукция_Элемент) = 1 Тогда
		    Продолжить;
		КонецЕсли;

		Состояние("Создание ВСД_Производство ");
		
		Если ФС.СуществуетФайл(ГМ.КаталогМодуля+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_Производство.ert")=1 Тогда
			
			//{ переопределение функции создания ВСД 
			// интеграция переопределяется в Меркурий_Подключаемый_Создание_ВСД_Производство.ert
			//
			
			СписокПараметров = СоздатьОбъект("СписокЗначений");
			СписокПараметров.ДобавитьЗначение(Отправитель_ХозСубъект,				"Отправитель_ХозСубъект");    
			СписокПараметров.ДобавитьЗначение(Отправитель_Площадка,					"Отправитель_Площадка");    
			СписокПараметров.ДобавитьЗначение( тзНеобходимыеПартии.КолвоСписания,			"Колво");    
			Попытка СписокПараметров.ДобавитьЗначение( тзНеобходимыеПартии.КолвоМестСписания,		"КолвоМест"); Исключение КонецПопытки;
			СписокПараметров.ДобавитьЗначение( тзНеобходимыеПартии.ВСД_Продукция_Элемент,	"Продукция_Элемент");    
			//
			СписокПараметров.ДобавитьЗначение( ВСД_Экспертиза,		"ВСД_Экспертиза");    
			СписокПараметров.ДобавитьЗначение( ВСД_Местность,		"ВСД_Местность");
			СписокПараметров.ДобавитьЗначение( ВСД_ОсобыеОтметки,	"ВСД_ОсобыеОтметки");    

			СписокПараметров.ДобавитьЗначение( НачДата,				"НачДата");
			СписокПараметров.ДобавитьЗначение( КонДата,				"КонДата");    
			
			ОткрытьФормуМодально("Отчет",СписокПараметров,ГМ.КаталогМодуля+"ПодключаемыеМодули\Меркурий_Подключаемый_Создание_ВСД_Производство.ert");
			
			ДокументСсылка = "";
			
			Если ТипЗначенияСтр(СписокПараметров)  = "СписокЗначений" Тогда
				
				ДокументСсылка = СписокПараметров.Получить("ДокументСсылка");
				
			КонецЕсли;  
			
			Если ПустоеЗначение(ДокументСсылка)=1 Тогда
				Предупреждение("В подключаемом модуле не удалось создать ВСД","!");
				//Возврат "";
			КонецЕсли;
			//}
		
		Иначе
			
			ДокВСД = СоздатьОбъект("Документ.ВСД_Производство");
			ДокВСД.Новый();
			ДокВСД.ДатаДок = НачДата;			
			
			ДокВСД.Производитель_ХозСубъект = Отправитель_ХозСубъект;
			ДокВСД.Производитель_площадка = Отправитель_Площадка;
			
			Автор = ГМ.ПолучитьАвтора();
			Попытка				
				ДокВСД.Автор = Автор;
			Исключение
			КонецПопытки;
			Попытка				
				ДокВСД.Филиал = Автор.Филиал;
			Исключение
			КонецПопытки;
			
			ДокВСД.Экспертиза 		= ВСД_Экспертиза;
			ДокВСД.Местность 		= ВСД_Местность;
			ДокВСД.ОсобыеОтметки 	= ВСД_ОсобыеОтметки;
			ДокВСД.cargoExpertized  = 1;
							
			ДокВСД.НоваяСтрока();
			
			ДокВСД.Продукция_Элемент 	= ВремСписокПартий.ВСД_Продукция_Элемент;
			ДокВСД.Количество 			= ВремСписокПартий.КолвоСписания;
			Попытка ДокВСД.КоличествоМест 		= ВремСписокПартий.КолвоМестСписания;Исключение КонецПопытки;					
			ДокВСД.ЕдиницаИзмерения 	= ДокВСД.Продукция_Элемент.ЕдиницаИзмерения;
			ДокВСД.ФормаУпаковки 		= ДокВСД.Продукция_Элемент.ФормаУпаковки;
			ДокВСД.Продукция 			= ДокВСД.Продукция_Элемент.Продукция;
			ДокВСД.ВидПродукции 		= ДокВСД.Продукция_Элемент.ВидПродукции;
			ДокВСД.ВидДвижения 			= 1;
			ДокВСД.НаименованиеПродукции = ДокВСД.Продукция_Элемент.Наименование;
			
			ДокВСД.ЗавершитьОперацию = 1;
			ДокВСД.ДатаИзготовления1 = НачДата;
			Если КонДата>НачДата Тогда
				ДокВСД.ДатаИзготовления2 = КонДата;
			КонецЕсли;
			ДокВСД.ДатаСрокГодности1 = ДокВСД.ДатаИзготовления1+ДокВСД.Продукция_Элемент.СрокГодности;
				
			
			ДокВСД.Записать();
			ДокументСсылка = ДокВСД.ТекущийДокумент();
		КонецЕсли;
		
		ВремСписокПартий.ВСД_Производство = ДокументСсылка;
		
		Сообщить(" создан документ "+ВремСписокПартий.ВСД_Производство,"i");		
	КонецЦикла;
	ВремСписокПартий.Выгрузить(тзНеобходимыеПартии);
КонецПроцедуры

//=========================== ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ =============================

//======================================================================
Процедура ОбработкаПодбора(Элемент, КонтФормы)
	Если Элемент.Вид()="ВСД_Площадка" Тогда
		СтараяПлощадка = СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Площадка");
		
		Если СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Площадка")= СтараяПлощадка Тогда 
			КонтФормы.Форма.Закрыть();    
			Возврат;
		КонецЕсли;
		
		СпрПлощадка = СоздатьОбъект("Справочник.ВСД_Площадка");
		Если ПустоеЗначение(СтараяПлощадка)=0 Тогда 
			Если СпрПлощадка.ВыбратьЭлементыПоРеквизиту("Склад",СтараяПлощадка.Склад,0,0)=1 Тогда
				Пока СпрПлощадка.ПолучитьЭлемент()=1 Цикл
					//Сообщить("Очистили привязку площадки "+СпрПлощадка.Контрагент);
					СпрПлощадка.Склад = "";
					СпрПлощадка.Записать();				
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		СпрПлощадка.НайтиЭлемент(Элемент);
		СпрПлощадка.Склад = СписокДокументов.ПолучитьЗначение(ВыбСтрока,"Грузополучатель");
		СпрПлощадка.Записать();
		КонтФормы.Форма.Закрыть();    
		
		СписокДокументов.УстановитьЗначение(ВыбСтрока,"Площадка", СпрПлощадка.ТекущийЭлемент());

		ТПДокументов.ОбновитьСтроки();
		
	ИначеЕсли Элемент.Вид()="ВСД_Партия" Тогда
		КонтФормы.Форма.Закрыть();    
		
		тзНеобходимыеПартии.УстановитьЗначение(ВыбСтрока,"Партия", Элемент);
		тзНеобходимыеПартии.УстановитьЗначение(ВыбСтрока,"Колво", Элемент.Количество);
		Попытка тзНеобходимыеПартии.УстановитьЗначение(ВыбСтрока,"КолвоМест", Элемент.КоличествоМест); Исключение КонецПопытки;				
		
		РаскраситьтзНеобходимыеПартии();				
	КонецЕсли;
	
КонецПроцедуры

//======================================================================
Процедура ПриИзмененииРазмераОкна(ТипСобытия, НовШирина, НовВысота) Экспорт
	ГМ._ПриИзмененииРазмераОкна(Контекст, ТипСобытия, НовШирина, НовВысота);
КонецПроцедуры

//======================================================================
Процедура ПриОткрытии()

	ВыбФирма = "";
      
	ГМ._ПриОткрытии(Контекст);
	
	ЗаголовокНадпись();   
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Перемещения");
	Форма.Закладки.ДобавитьЗначение("Партии");
	Форма.Закладки.ДобавитьЗначение("Параметры");
	          
	Форма.ИспользоватьСлой("Основной, СписокДокументов, Перемещения");
	
	Партия = "";
	
	Парам = Форма.Параметр;
	Если ТипЗначенияСтр(Парам)="СписокЗначений" Тогда  
		
		Если ПустоеЗначение(Парам)=0 тогда 
			Для Д=1 По Парам.РазмерСписка() Цикл
				Док = Парам.ПолучитьЗначение(Д);
				СписокДокументов.НоваяСтрока();
				СписокДокументов.Пометка= 1;
				СписокДокументов.Док = Док;
				СписокДокументов.Колво = Док.ВесДокумента;
				СписокДокументов.Вес = Док.ВесДокумента;
				СписокДокументов.КолвоМест = Док.КоличествоМестДокумента;
				СписокДокументов.Контрагент = Док.Контрагент;
				СписокДокументов.ХозСубъект = ГМ.НайтиХозСубъект(СписокДокументов.Контрагент);
				СписокДокументов.Площадка = ГМ.НайтиПлощадкуПоКонтрагенту(СписокДокументов.Грузополучатель);
			КонецЦикла;

			ТПДокументов.ОбновитьСтроки();
			
			ВыделитьВсеДокументы();	
		КонецЕсли;
	Иначе
		//стандартное заполнение документов
		ЗаполнитьСписокДокументов();
	КонецЕсли;   
	
	флФильтрПоСкладу = ВосстановитьЗначение("ФильтрПоСкладу");

	Если Форма.МодальныйРежим() = 0 Тогда
		оПривязки.Привязка("ТПДокументов", "H", "Форма", "W", "Форма");
		оПривязки.Привязка("тзНеобходимыеПартии", "H", "Форма", "W", "Форма");
		оПривязки.Привязка("кнЗакрыть","T","Форма");
	КонецЕсли;

КонецПроцедуры

//======================================================================
Процедура ПослеОткрытия()
	ГМ._ПослеОткрытия(Контекст);
КонецПроцедуры

//======================================================================
Процедура ПослеСозданияФормы()
	ТПДокументов = ГМ.СоздатьТабличноеПоле(Контекст, "ТПДокументов", СписокДокументов,,1);
КонецПроцедуры

//******************************************************************************
 // предопределенная процедура
 //
 Процедура ПриВыбореЗакладки(НомерЗакладки, ЗначениеЗакладки)
	
 	ТекСтрока = СписокДокументов.ТекущаяСтрока();
 	
 	Если ЗначениеЗакладки="Перемещения" Тогда
		Форма.ИспользоватьСлой("Основной, СписокДокументов");
	ИначеЕсли ЗначениеЗакладки="Партии" Тогда
		Форма.ИспользоватьСлой("Основной, Партии");
	Иначе
		Форма.ИспользоватьСлой("Параметры");
	КонецЕсли;     
	
КонецПроцедуры 

//ЖД доп
Процедура ПриИзмененииПлощадки()
	//Обновить список документов 
	ГМ.СписокКонстант.Установить("Отправитель_Площадка",Отправитель_Площадка);	
	
	СписокДокументов.УдалитьСтроки();
	тзНеобходимыеПартии.УдалитьСтроки();		
	ЗаполнитьСписокДокументов();
КонецПроцедуры

//======================================================================
Процедура ПриЗакрытии()
	СохранитьЗначение("ФильтрПоСкладу", флФильтрПоСкладу);
КонецПроцедуры // ПриЗакрытии

Процедура МенюВСД()
	спМеню = СоздатьОбъект("СписокЗначений");
	спМеню.ДобавитьЗначение("СоздатьВСД", "Создать ВСД");
	спМеню.ДобавитьЗначение("ОтправитьВСД", "Отправить ВСД");
	спМеню.ДобавитьЗначение("АннулироватьВСД", "Аннулировать ВСД");
	зн = "";
	спМеню.ВыбратьЗначение(зн,,,,1);
	Если зн = "СоздатьВСД" Тогда 
		СоздатьВСД();
	ИначеЕсли зн = "ОтправитьВСД" Тогда 
		ОтправитьВСД();
	ИначеЕсли зн = "АннулироватьВСД" Тогда 
		АннулироватьВСД();
	КонецЕсли;
		
КонецПроцедуры


ТПДокументов = "";
НачДата = ТекущаяДата();
КонДата = ТекущаяДата();

СписокДокументов = СоздатьОбъект("ТаблицаЗначений");
СписокДокументов.НоваяКолонка("Пометка",,,,,5,);
СписокДокументов.НоваяКолонка("Грузополучатель",,,,,70,);
СписокДокументов.НоваяКолонка("Площадка",,,,,30,);
СписокДокументов.НоваяКолонка("ВСД",,,,,10,);
СписокДокументов.НоваяКолонка("Колво",,,,,10,);
СписокДокументов.НоваяКолонка("Вес",,,,,10,);
СписокДокументов.НоваяКолонка("КолвоМест",,,,,10,);
СписокДокументов.НоваяКолонка("Док",,,,,30,);
СписокДокументов.НоваяКолонка("Контрагент",,,,,10,);
СписокДокументов.НоваяКолонка("ХозСубъект",,,,,10,);

СписокПартий = СоздатьОбъект("Таблицазначений");
СписокПартий.НоваяКолонка("ДокРеализации",,,,,20,);
СписокПартий.НоваяКолонка("Номенклатура",,,,,20,);
СписокПартий.НоваяКолонка("ВСД_Продукция_Элемент",,,,,20,);
СписокПартий.НоваяКолонка("Партия",,,,,15,);
СписокПартий.НоваяКолонка("ПолеСортировки",,,,,10,);
СписокПартий.НоваяКолонка("Колво","Число",,,,5,);
СписокПартий.НоваяКолонка("КолвоСписания","Число",,,,5,);

ЦвЖелтый 	= "FONT[0]BRUSH[65535]FONT_S[0]BRUSH_S[65535]                       ";
ЦвЗеленый 	= "FONT[0]BRUSH[65280]FONT_S[0]BRUSH_S[65280]                       ";
ЦвКрасный  	= "FONT[0]BRUSH[255]FONT_S[0]BRUSH_S[255]                           " ;
ЦвГолубой	= "FONT[0]BRUSH[13421619]FONT_S[0]BRUSH_S[13421619]                 ";
ЦвФиолетовый= "FONT[0]BRUSH[11665663]FONT_S[0]BRUSH_S[11665663]";

ПартияИнфо = "<<выберите партию>>";
